//chrissstrahl - made compatible to coop

//chrissstrahl - made multiplayer compatible 
//2018.07.24

// specify a target entity (must have a head), a range of variance, and an offset (ie can't aim any closer than this offset)
//======================================================
main local.targetEnt local.range local.offset:{
//======================================================
	//[200] chrissstrahl - fixed usage of deprecated var
	//[200] chrissstrahl - fixed logic
	if( level.gametype != 0 ){
		if( exec coop_mod/replace.scr::isPlayerArray local.targetEnt ){
			local.targetEnt = exec coop_mod/replace.scr::player_closestTo self
			while( local.targetEnt == NULL){
				if( self == NULL ){ end }
				local.targetEnt = exec coop_mod/replace.scr::player_closestTo self
				waitframe
			}	
		}
	}
	
	//[203] chrissstrahl - fixed check
	if( !local.targetEnt || !$(local.targetEnt) || !(isalive self) ){ end }
	
	local.a = $(local.targetEnt) gettagposition "bip01 head"

	for (local.i=0;local.i<3;local.i++){
		if (randomint(100) > 50){
			local.a[local.i] = local.a[local.i] + randomint(local.range) + local.offset
		}
		else{
			local.a[local.i] = local.a[local.i] - randomint(local.range) - local.offset
		}
	}
	self exec global/aimat.scr local.a
}end
