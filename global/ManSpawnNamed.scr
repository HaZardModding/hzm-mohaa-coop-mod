//chrissstrahl - modified for coop on 2020.01.03

//
// ManSpawnNamed
//


Init local.name:

	if (!$(local.name))
	{
		println ("ManSpawnNamed::Init - no guys of name " + local.name)
		end
	}

	local.num = $(local.name).size
	local.array = exec global/makearray.scr $(local.name)

	for (local.i=1; local.i<=local.num; local.i++)
	{
		local.saved_angles = local.array[local.i].angles

		local.ent = waitthread global/spawner.scr::spawner_create local.array[local.i]

		local.ent.saved_angles = local.saved_angles
		local.ent.targetname = (local.name + "_manspawnnamed")
	}
	//[200] chrissstrahl - disabled, because it spams the console
	//println ("ManSpawnNamed::Init - guys called " + local.name + " are changed to spawners")
end


Spawn local.name:

	local.sname = local.name + "_manspawnnamed"

	if (!$(local.sname))
	{
		println ("ManSpawnNamed::Spawn - no guys of name " + local.name)
		end
	}

	local.num = $(local.sname).size
	local.array = exec global/makearray.scr $(local.sname)

	for (local.i=1; local.i<=local.num; local.i++){
		local.ent = waitthread global/spawner.scr::spawner_activate local.array[local.i]

		local.ent.targetname = local.name
		local.ent.origin = local.array[local.i].origin
		local.ent.angles = local.array[local.i].saved_angles
		local.ent forceactivate

// Used to do this, but instead, let's allow the spawner to stick around.  That way, spawners can be used multiple times!
//		local.array[local.i] remove
	}

	//[200] chrissstrahl - disabled, because it spams the console
	//println ("ManSpawnNamed::Spawn - guys called " + local.name + " are spawned")
end



