//////////////////////////////////////////////
// started by chrissstrahl on 2020.10.16 ([202])
// 
// Contains player related functions
//
//////////////////////////////////////////////

//this gets everything ready to manage players
//=========================================================================
main:{
//=========================================================================
	//start manager for players
	thread manage
}end

//this manages players previously done by: main.scr::managePlayers
//=========================================================================
manage:{
//=========================================================================
	//wait for any player to join server
	level waittill spawn
	
	//remove placeholder player - relict from singleplayer
	waitthread removeFakePlayer
	
	//[200] Smithy - reset any vars that may have transferred from previous map
	waitthread resetPlayers
	
	//[202] Smithy - moved out from inside loop //[200] chrissstrahl - used to detect when a player leaves/joins
	local.prevPlayerCount = 0
	
	//[202] chrissstrahl - handle players
	while(1){	
		//[202] chrissstrahl - if server is empty wait
		if ($player.size == 0){
			waitframe
			continue
		}
		
		//[202] chrissstrahl - handle all players
		for (local.i = 1;local.i <= $player.size;local.i++){
			local.player = $player[local.i]
			
/////////////////////////////////////////SPECTATOR
			if( local.player.dmteam == "spectator" ){
				waitthread manageSpectator local.player
			}
////////////////////////////////////////NO SPECTATOR TEAM
			else{
				//this makes sure that the player does not get spawned anywhere in the map
				//where he was spectating at, the moment the player spawns he needs to be
				//already at the spawnlocation or he can telefrag a player, friendly or enemy
				if(local.player.flags["coop_isActive"] == -1){
					commanddelay .1 iprintlnbold_noloc("PLAYER HIT BACKUP SPAWN") //[202] Smithy - i dont believe this will ever hit if spawnEvent takes care of it
					//local.player.flags["coop_isActive"] = 0
					//waitthread coop_mod/main.scr::playerPlaceAtSpawn local.player
				}
				waitthread manageNoSpectator local.player
/////////////////////////////////////////ALIVE
				if (!local.player.isSpectator && local.player.health > 0){ //[202] Smithy - fixed players being able to respawn anywhere
					//spawning is event based handled in: main.scr::playerSpawnEvent
					waitthread manageAlive local.player local.i
					
					//[202] Smithy - 'isSpectator' also checks for temp spectators (very useful).
					//i.e. those who have died and are floating around but not actually in spectator team.
					//see game code:					
					//bool Player::IsSpectator( void )
					//{
					//	return ( m_bSpectator || m_bTempSpectator );
					//}		
					//m_bSpectator = team spectator
					//m_bTempSpectator = floaty dude but not on spec team
				}
/////////////////////////////////////////DEAD
				//death is event based handled in: main.scr::playerDeathEvent
				
				
				//@chrissstrahl seperate func and probably rework
/////////////////////////////////////////RESTORE FOV IF NAME IS CHANGED
				//prevent this thread to be executed several times
				if(	local.player.flags["coop_netname"] != local.player.netname &&
					level.time > (local.player.flags["coop_netnameChanged"] + (game.frame * 3))
					/*&& local.player.flags["coop_setupComplete"] == 1*/)
				{
					thread manageNamechange local.player
				}
			}
		}
		
		//[202] Smithy - moved here and done in one //[200] chrissstrahl - used for LMS to detect when a player leaves
		local.playerCount = $player.size
		
		//[200] chrissstrahl - call player changed func if number of players changed
		if(local.prevPlayerCount != local.playerCount){
			waitthread coop_mod/main.scr::playerCountChanged local.prevPlayerCount local.playerCount
		}

		//[200] chrissstrahl - LMS - check if condistions have changed
		waitthread coop_mod/main.scr::lmsCheckLives local.prevPlayerCount local.playerCount

		//[200] chrissstrahl - keep track of player count
		local.prevPlayerCount = local.playerCount
	
		//[202] chrissstrahl - all this needs to be done only once per server frame (sv_fps)
		waitframe
	}
}end

//does first time setup on a player
//called from ::manage if local.player.flags["coop_isActive"] == NIL
//=========================================================================
manageSetup local.player:{
//=========================================================================
	//once all setup is done these vars are set:
	//local.player.flags["coop_setupComplete"] = 1

	if(local.player.flags["coop_isActive"] != NIL){
		//println("manageSetup::::::::::::ALREADY RUN")
		end
	}
	//println("manageSetup::::::::::::RUNNING")
	local.player.flags["coop_isActive"] = -3
	
//Host detection
	if(!level.coop_dedicated){
		//[200] Smithy - as this is a bool, just do it in one line
		local.player.flags["coop_isHost"] = local.player.entnum == 0
	}

	//[202] Smithy - store client 0
	if (local.player.entnum == 0){
		level.coop_clientZero = local.player
	}
	
//RUN OTHER SETUPS
	//RETRIVE DATA FROM NAME
	thread setupCmds local.player

//LMS reconnect protection
	//During LMS add deaths to prevent player from playing if he is not allowed to
	//New players are not allowed in if one player left before on the current map
	//this prevents players from just reconnecting when dead
	if( level.coop_lmsAllowJoin != 1){
		local.lmsLives = level.coop_lmsLives
		if(local.lmsLives > 0){
			local.player.flags["coop_deaths"] = local.lmsLives
		}
	}
	
//SPECTATOR forceing
	//force player in spec to prevent any issues
	//WARNING:	WARNING:	WARNING:	WARNING:
	//the code depends on vars set and code executed
	//the same stuff is done when entering spectator
	//so the following code was disabled but keept for reference:
	//	local.player.fnum = -100
	//	local.player.flags["coop_iAmTruelyActive"] = 0
	//	local.player.flags["coop_isActive"] = -1
	//	local.player.flags["coop_justSpawnedDone"] = NIL
	//	waitthread coop_mod/main.scr::resetSpawn local.player
	
//VARIABLES initialization 
	local.player.flags["coop_lastRespawn"] = -1
	local.player.flags["coop_joinedGameAt"] = level.time
	local.player.flags["coop_netnameChanged"] = -1

//SPACETAOR - play it save put that thing into spectator
	local.player spectator
}end

//[202] chrissstrahl - for changing a player's FOV with a delay based on data from name
//=========================================================================
manageNamechange local.player:{
//=========================================================================
	local.playerNetnameFull = local.player.netname
	local.player.flags["coop_netname"] = local.player.netname 
	
	//make sure player name is even capable of handling player name
	local.datasize = 7
	local.maxcharsize = (31 - local.datasize)//31=maxchars in name, 8=chars for key+command
	if(local.playerNetnameFull.size > (31 - local.datasize)){ //length is " ,1data"
		local.player iprint ("Your Name is to long ("+local.playerNetnameFull.size+") for commands to work in Coop ("+local.maxcharsize+")...") 1
	}
	
	//check if we can find certain data in name
	//ADD ONLY COMMANDS - that we want truly to be executable at all times
	local.command["0"]=" ,1" //fov NOTE: ,0 is reseved
	local.command["1"]=" ,2" //teleport
	local.command["2"]=" ,3" //developer commands
	local.command["3"]=" ,stuck" //developer commands
	local.commandNumber=( -1 ) //on purpose out of range
	//@chrissstrahl //@smithy - need function to check all local.command for ocuurence of words like stuck (probably with main.scr::containsText)
	
	local.i=0;
	while(local.command[string(local.i)]){
		local.find = waitthread coop_mod/main.scr::containsText local.playerNetnameFull local.command[string(local.i)] 1
		if(local.find){
			local.commandNumber = local.i
			break
		}
		local.i++
	}

	//check if datatype is supported
	local.datatype = local.command[string(local.commandNumber)]
	
	//println("manageNamechange:1 - "+local.find+":"+string(local.datatype))
	
	//only supported datatypes are handled
	if(local.datatype != NIL){
		local.extract = waitthread extractDataFromNetname local.player local.datatype
		//println("manageNamechange:2 - NETNAME:"+local.playerNetnameFull+";")
		//println("manageNamechange:2 - DATATYPE:"+string(local.datatype)+";")
		//println("manageNamechange:2 - Extract:"+string(local.extract)+";")
		
		//handle datatypes
		//fov - is handled below it has to be set with delay
		
//datatype teleport///////////////////////////////////////////////////
//datatype teleport///////////////////////////////////////////////////
//datatype teleport///////////////////////////////////////////////////
		if(local.datatype == local.command["1"]){//teleport
			//iprintlnbold_noloc("TRANSPORT DATA: "+local.extract)
			//START - player wants to go spawn location
			if(local.extract == "start"){
				thread coop_mod/main.scr::playerPlaceAtSpawn local.player
			}
			//PLAYERNAME / NUMBER - if it is non of the above check if it is a player name
			else{
				local.validPlayerFound = 0
				//MATCHING PLAYER NAME
				for (local.i = 1;local.i <= $player.size;local.i++){
					local.playerOther =  $player[local.i]
					if(local.playerOther == local.player){ continue } //disabled during testing
					
					//a player with a matching or similar name was found - move player there
					if( waitthread coop_mod/main.scr::containsText local.playerOther.netname local.extract ){
						local.validPlayerFound = 1
						local.player origin local.playerOther.origin
						thread coop_mod/main.scr::playerMakeSolidAsap local.player
						thread coop_mod/main.scr::playerMakeSolidAsap local.playerOther
					}
				}
				//MATCHING ENTITY NUMBER
				if(local.validPlayerFound == 0 && int(local.extract) > -1 && int(local.extract) < int(getcvar("sv_maxclients"))){
					local.playerOther = getentbyentnum( 0 )
					if( 	local.playerOther != NULL &&
							local.playerOther != local.player &&
							local.playerOther.health > 0 &&
							local.playerOther.health > 0 &&
							local.playerOther.classname == "player" &&
							local.playerOther.dmteam != "spectator")
					{
						local.validPlayerFound = 1
						local.player origin local.playerOther.origin
						thread coop_mod/main.scr::playerMakeSolidAsap local.player
						thread coop_mod/main.scr::playerMakeSolidAsap local.playerOther
					}
				}
				//NO VALID PLAYER FOUND
				if(local.validPlayerFound == 0){
					local.player iprint "No valid player found. Try with different parameter." 1
				}
			}
		}
//datatype dev///////////////////////////////////////////////////
//datatype dev///////////////////////////////////////////////////
//datatype dev///////////////////////////////////////////////////
		else if(local.command["2"] == local.datatype){
			if(int(getcvar("coop_dev")) <= 0 || !local.player.flags["coop_isHost"] ){ end }
			local.player thread coop_mod/developer.scr::devcommand local.extract
		}
//stuck command///////////////////////////////////////////////////
//stuck command///////////////////////////////////////////////////
//stuck command///////////////////////////////////////////////////
		else if(local.command["3"] == local.datatype){ //,stuck //@chrissstrahl //@smithy - need function to check all local.command for ocuurence of words like stuck (probably with main.scr::containsText)
			//@chrissstrahl - add handle here that moves player to next spawn each time it is used, start at 1
		}
	}else{
		println("manageNamechange: unsuported data key type for player: "+local.player.netname )
	}
	
	//every time name is changed fov needs to be set again
	//we need a delay before this or it will be reset right away
	waitframe
	waitframe
	//only change fov if player was indeed renamed
	if(local.player){
		//datatype fov
		if(local.datatype == local.command["0"] && local.extract != NIL){
			local.fov = int(local.extract)
		}else{
			local.fov = local.player.flags["coop_fov"]
		}
		thread manageFovSet local.player local.fov
	}
}end

//[200] Smithy - for changing a player's FOV
//[202] chrissstrahl - moved renamed and changed
//=========================================================================
manageFovSet local.player local.fov:{
//=========================================================================
	local.threadName = "coop_mod/main.scr::changePlayerFOV"
	//[202] chrissstrahl - don't accept bad range
	if(!local.fov || local.fov < 10){
		end
	}
	
	//println("manageFovSet::::::::::::"+local.fov)
	//[202] chrissstrahl - update cvars first to prevent resetting the fov
	if(local.player){
		local.player.flags["coop_fov"] = local.fov
		local.player stufftext ("seta g_m2l2 "+local.fov)
	}
	
	local.prevCvar = getcvar "developer"
	setcvar "developer" "1"
	waitthread coop_mod/main.scr::changeGameType 0 0	
	local.player fov local.fov
	waitthread coop_mod/main.scr::changeGameType 2 0
	setcvar "developer" local.prevCvar
}end

//spectator handle
//called from ::manage if player is spectator
//=========================================================================
manageSpectator local.player:{
//=========================================================================
//SETUP when entering spectator
	//run only once per spectator join
	if(	local.player.flags["coop_isActive"] != -1 ){
		if(	local.player.flags["coop_isActive"] == NIL || local.player.flags["coop_iAmTruelyActive"] == NIL){
			//println("manageSpectator::::::::::::")
			waitthread manageSetup local.player
		}
	
		//Notify scripts instantly this player is no longer valid
		local.player.flags["coop_isActive"] = -1
		local.player.flags["coop_iAmTruelyActive"] = 0
		local.player.flags["coop_justSpawnedDone"] = NIL
		
		//this is used for friendly orientation see global/friendly::playernode
		local.player.fnum = -100
		
		local.player unglue
		local.player threatbias ignoreme		

		waitthread coop_mod/main.scr::resetSpawn local.player
		thread manageFovSet local.player (int(string(local.player.flags["coop_fov"])))
	}
//WHILE in spectator
	//do not allow player in just right away, wait for next handle (1 frametime)
	else{
		//[200] Smithy - now we just check for fire button to auto join them to allies and skip weapon menu
		//[200] Smithy - put here to stop spam that prevents players typing/using menu
		if (local.player.primaryfireheld){
			thread coop_mod/main.scr::skipTeamAndWeaponSelect local.player
		}
	}
}end

//no spectator player handling
//called from ::manage if player is not a spectator
//=========================================================================
manageNoSpectator local.player:{
//=========================================================================
//PHYSICS
	if (level.coop_physicsOff){ local.player physics_off }
}end

//alive player handling
//called from ::manage if player is not a spectator and has positive health
//=========================================================================
manageAlive local.player:{
//=========================================================================
//Code that is executed upon spawning is now in:
//coop_mod/main.scr::playerSpawnEvent

// SAVE CURRENT LOCATION FOR RESTORING ON RESPAWN /////
	if( level.coop_disableSpawnWarper != TRUE && !level.coop_playerGlue ){
		waitthread manageAliveSaveLocation local.player
	}
}end

//alive Spawning player handling
//called from main.scr::playerSpawnEvent if player just spawned
//=========================================================================
manageAliveSpawning local.player:{
//=========================================================================
	//println("manageAliveSpawning")
	/////////////////////////////////////////SETUP
	if(	local.player.flags["coop_isActive"] == NIL){
		waitthread manageSetup local.player
	}

	//isSpectator is a built-in BT bool, more efficient than checking for dmteam string
	//force them into allies if they spawned as axis and abort current handling
	if( local.player.dmteam == "spectator" || thread coop_mod/main.scr::forceValidTeam ){
		//reset the respawn location, makes player spawn at predefined location
		waitthread coop_mod/main.scr::resetSpawn local.player
		end
	}
	
	//LAST MAN STANDING
	if( thread coop_mod/main.scr::lmsForceSpectatorHandle local.player ){
		end
	}
	
	// RESPAWNING /////////////////////////////////////////
	//place at spawn or respawn, this needs to be done regardless of glueing
	if(local.player.flags["coop_respawning"] == TRUE){
		waitthread coop_mod/player.scr::manageAliveRespawning local.player
		thread coop_mod/main.scr::startThread ( "maps/"+level.coop_mapname+".scr::coop_playerJustRespawned" ) (local.player)
	}else{	
		waitthread coop_mod/main.scr::playerPlaceAtSpawn local.player
		thread coop_mod/main.scr::startThread ( "maps/"+level.coop_mapname+".scr::coop_playerJustSpawned" ) (local.player)
	}
	
	//INVENTORY
	waitthread coop_mod/itemhandler.scr::managePlayerInventory
	thread coop_mod/main.scr::itemGetAll
	thread coop_mod/main.scr::playerHealth	
	
	//GLUE
	thread coop_mod/main.scr::playerGlue local.player "spawn"	
	
	//NO SPEC
	if(local.player.dmteam == "spectator"){
		end
	}

	//VARIABLES
	local.player.fnum = -1		//this is used for friendly orientation see global/friendly::playernode
	local.player.flags["coop_iAmTruelyActive"] = 1
	local.player.flags["coop_isActive"] = 1
	local.player.flags["coop_isAlive"] = game.true
	local.player threatbias level.coop_threatbias
	local.player solid
	local.player american
	
	//HIDE PLAYER
	if( level.coop_playerHide ){
		local.player hide
	}
	
	//[202] chrissstrahl - implement script fov
	thread manageFovSet local.player (int(string(local.player.flags["coop_fov"])))
	
	//PLAYER READY
	//[202] chrissstrahl	
	//a player has joined a team and picked a weapon
	//notify other scripts that the mission can now start
	level.coop_playerReady = 1
}end

//alive respawning player handling
//called from player.scr::manageAliveSpawning if player just respawned
//=========================================================================
manageAliveRespawning local.player:{
//=========================================================================
	//println("manageAliveRespawning")
	//respawn if allowed at last pos before death of player
	//also do not respawn if coop_respawnOrigin flag is set to 0 0 0 or something very close
	if( level.coop_disableSpawnWarper != TRUE && vector_length( local.player.flags["coop_respawnOrigin"] ) > 10){
		
		//make player solid as soon as possible (checks if inside other players or friendly)
		thread coop_mod/main.scr::playerMakeSolidAsap local.player
		
		//remember when we last respawned, prevent placeing to close at spawnlocation
		local.player.flags["coop_lastRespawn"] = level.time 
		local.player origin ( local.player.flags["coop_respawnOrigin"] )
		
		//set angle - need to call this delayed or it will not work most of the time in mp
		if(local.player.flags["coop_respawnAngle"] != 0){
			local.player thread coop_mod/main.scr::playerFace ( 0 local.player.flags["coop_respawnAngle"] 0 )
		}
	}
	//location restore is off or not possible. spawn at predefined spawnlocation
	else{
		thread coop_mod/main.scr::playerPlaceAtSpawn local.player
	}
	local.player.flags["coop_respawning"] = FALSE
}end

//alive player handling - used to save location on player
//called from ::manageAlive if
// player is not a spectator and has positive health
// player has flag set: local.player.flags["coop_respawning"] != TRUE
// if global var level.coop_disableSpawnWarper != TRUE
//=========================================================================
manageAliveSaveLocation local.player:{
//=========================================================================
	if(	local.player.flags["coop_respawnTime"] == NIL || local.player.flags["coop_respawnTime"] < (level.time + 2) ){
		local.player.flags["coop_respawnTime"]		= level.time
		local.player.flags["coop_respawnOrigin"]	= local.player.origin
		local.player.flags["coop_respawnAngle"]		= local.player.viewangles[1]
	}
}end

//handles dead player
//called from ::manage if player health is negative or 0
//=========================================================================
manageDead local.player:{
//=========================================================================
	//println("manageDead")
	local.player.flags["coop_iAmTruelyActive"] = 0
	if(	local.player.flags["coop_isActive"] != -2 ){
		//[202] chrissstrahl - implement script fov
		thread manageFovSet local.player (int(string(local.player.flags["coop_fov"])))
	
/// LMS Handle
		if( level.coop_lmsLives > 0 ){
			//handle death count
			local.deaths = local.player.flags["coop_deaths"]
			local.deaths++
			local.player.flags["coop_deaths"] = local.deaths
			
			//print info to player hud
			local.deaths = (level.coop_lmsLives - local.deaths)
			if(local.deaths < 0){local.deaths=0}
			
			local.deaths = ( "You have [ " + local.deaths + " ] lives left - LastManStanding is active!" ) //[201] chrissstrahl - fixed typo
			local.player iprint local.deaths 0
		}
		thread coop_mod/main.scr::lmsForceSpectatorHandle local.player
		
// REGULAR dead handle
		local.player.flags["coop_isActive"] = -2
		local.player.flags["coop_justSpawnedDone"] = NIL
		local.player.flags["coop_respawning"] = TRUE
		local.player.fnum = -100
		
		local.player unglue
		local.player physics_on
		local.player threatbias ignoreme
		
		//[200] chrissstrahl - start thread in main level-script
		//used to handle stuff when player dies
		//used to make sure player is not respawning in a vehicle or something
		//
		//There are these optional functions calls:
		//- FIRST SPAWN:	coop_playerJustSpawned
		//- EACH RESPAWN:	coop_playerJustRespawned
		//- EACH DEATH:		coop_playerJustDied
		thread coop_mod/main.scr::startThread ( "maps/"+level.coop_mapname+".scr::coop_playerJustDied" ) (local.player)
	}
}end

//[200] Smithy - fresh dedi servers will have a fake player on startup.
//we don't need it, and it may mess first client up that takes its spot
//e.g. 'press fire to join game' doesn't show for first client to join
//server.
//This fake player may also mess with map restarts (e.g. missionfailed)
//=========================================================================
removeFakePlayer:{
//=========================================================================
	if (level.coop_dedicated){
		if ($player && $player[1].netname == "*** Blank Name ***" && $player[1].entnum == 0){
			$player[1] delete
		}		
	}
}end

//[200] Smithy - on listen servers vars can carry over from map to map
//=========================================================================
resetPlayers:{
//=========================================================================
	for (local.i = 1; local.i <= $player.size; local.i++){
		$player[local.i].flags["coop_isActive"] = NIL
	}
}end

//[200] chrissstrahl - handle setup cmds for this player
//this has to be seperate here
//=========================================================================
setupCmds local.player:{
//=========================================================================
	if(local.player == NIL || local.player== NULL){
		error ("coop_mod/main.scr::setupCmds Fatal - Invalid player")
		end
	}
	
	//we need to give the host some more time, because he is running all the scripts and calculations
	if(local.player.flags["coop_isHost"] == 1){
		wait 0.25
	}
	//wait 0.5
	
	if(local.player == NIL || local.player== NULL){
		error ("coop_mod/main.scr::setupCmds Fatal - Invalid player")
		end
	}	
	
	//DETECT FOV AND MOD VERSION
	waitthread detectCoopFov local.player
	waitthread detectCoopMod local.player

	//this is used for playing current music, if the player joins mid-game
	//i suspect we only need to have one command, because tmstart/tmloop are overwriting each other
	if( level.coop_musicCommand != NIL ){
		local.player stufftext ( level.coop_musicCommand )
	}
	//this is used to set volume of current music, if the player joins mid-game
	if( level.coop_musicCommandVol != NIL ){
		local.player stufftext ( level.coop_musicCommandVol )
	}

	//local.player stufftext ( "tmstop" ) //[200] chrissstrahl - moved to detect.cfg
	
	//this is used to set current music loop, if the player joins mid-game
	//if( level.coop_musicCommandLoop != NIL ){
		//local.player stufftext ( level.coop_musicCommandLoop )
	//}
	
	//ALL SETUP COMPLETE
	waitframe
	local.player.flags["coop_setupComplete"] = 1
}end

//[202] chrissstrahl - detect data in name
//=========================================================================
extractDataFromNetname local.player local.key:{
//=========================================================================
	if(!local.player){ end NULL }
	
	local.key = string(local.key)

	//CHECK IF DATA CAN BE FOUND
	local.fullNetName = string(local.player.netname);
	local.dataStart = waitthread coop_mod/main.scr::containsText local.fullNetName (local.key) game.true
	
	//NO DATA IN NAME, UPDATE VAR and do not rename
	if(!local.dataStart){
		local.player.flags["coop_netname"] = local.fullNetName
		//println("extractDataFromNetname:NIL:")
		end NIL
	}
	
	//RESTORE PLAYER NAME
	local.cleanName = ""
	for(local.current = 0; local.current < local.dataStart;local.current++){
		local.cleanName += string(local.fullNetName[local.current])
	}
	
	local.player.flags["coop_netnameChanged"] = level.time
	local.player.flags["coop_netname"] = local.cleanName
	local.player stufftext ("set name "+local.cleanName+"\n") //chrissstrahl - this requires the set Command to handle spaces in names
	//println("extractDataFromNetname clean:"+local.cleanName+":")
	
	//EXTRACT DATA FROM NAME - skip WHITHESPACE+COLON
	local.dataStart += (local.key.size)
	local.dataExtracted = ""
	for(local.current = local.dataStart; local.current < local.fullNetName.size;local.current++){
		local.currentChar = string(local.fullNetName[local.current])
		if(local.currentChar){
			local.dataExtracted += string(local.fullNetName[local.current])
		}
	}
	//println("extractDataFromNetname END:"+local.dataExtracted+":")
}end local.dataExtracted

//[202] chrissstrahl - detect fov in name
//=========================================================================
detectCoopFov local.player:{
//=========================================================================
	//[200] chrissstrahl - update menu entry
	local.player stufftext ( "vstr g_m2l1\n" )
	
	//detect coop mod version
	local.waitFrames = (1 * int(getcvar("sv_fps")))
	while(local.player && local.waitFrames > 0){
		local.extract = waitthread extractDataFromNetname local.player " ,1" //01=fov,02=teleport
		if(local.extract != NIL){
			local.fov = int(local.extract)
			thread manageFovSet local.player local.fov
			break
		}
		local.waitFrames--
		waitframe
	}
}end

//[202] chrissstrahl - detect coop version
//=========================================================================
detectCoopMod local.player:{
//=========================================================================
	//[200] chrissstrahl - used to detect if a player has the coop mod installed
	local.player stufftext ( "exec coop_mod/cfg/detect.cfg\n" )
	
	//detect coop mod version
	//detect coop mod version
	local.waitFrames = (1 * int(getcvar("sv_fps")))
	while(local.player && local.waitFrames > 0){
		local.extract = waitthread extractDataFromNetname local.player " ,0" //00=coop mod
		if(local.extract != NIL){
			local.player.flags["coop_installed"] = 1
			local.player.flags["coop_version"] = int(local.extract)
			break
		}
		local.waitFrames--
		waitframe
	}
}end

//[202] chrissstrahl - force 3rd Person view
//=========================================================================
set3rdPerson local.player local.activate:{
//=========================================================================
	local.prevCvar = getcvar "developer"
	setcvar "developer" "1"
	waitthread coop_mod/main.scr::changeGameType 0 0
	local.player stufftext ( "set cg_3rd_person "+local.activate+"\n" )
	//waitframe
	waitthread coop_mod/main.scr::changeGameType 2 0
	setcvar "developer" local.prevCvar
	iprintlnbold_noloc("set3rdPerson: "+local.activate)
}end

//[202] Smithy - checks if client zero exists, if not reconnect a player
//=========================================================================
checkClientZero:{
//=========================================================================
	if ($player && !waitthread isClientZeroActive){
		thread reconnectPlayer $player[1]
	}
}end

//=========================================================================
isClientZeroActive: end(level.coop_clientZero != NULL)
//=========================================================================

//[202] Smithy - make a player reconnect to server
//we can use this to fix player 0 issue
//=========================================================================
reconnectPlayer local.player:{
//=========================================================================
	local.player stufftext "disconnect; wait 1; reconnect" //needs work, dont use yet
}end
