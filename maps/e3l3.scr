//props
// $steal_ab_trigger  --  trigger once that adds the steal the ab41 objective and makes it current

//=========================================================================
main: {
//=========================================================================
	waitexec maps/e3l3/hacks.scr
	level.tunneltruckhealth = 500
	exec global/auto.scr e3l3

	level waittill prespawn

	thread maps/e3l3/scene1.scr::InitScene1  //the trucks
	thread maps/e3l3/scene2.scr::InitScene2  //the 1st k5
	thread maps/e3l3/scene3.scr::InitScene3  //the ab41 & 2nd k5

	//$player takeall	//MiaCosa
	exec coop_mod/replace.scr::takeAll
	exec global/fadein.scr

	level waittill spawn
	waitthread InitLevel

	if (level.startonAB41 == 1) {
		level.AB41Ready =  1
		thread gags/e3l3_ab41.scr::GetOnPathVehicle
	}

	if (level.skipobj1 == 0) {
		waitthread maps/e3l3/scene1.scr::DoObjective1   //part of scene1
	}

	// println "trying to set the tunnel truck"

	if (level.skipobj2 == 0) {
  		waitthread maps/e3l3/scene1.scr::DoObjective2    //ends scene 1
	}
	
	waitthread maps/e3l3/scene1.scr::CleanupScene1

	// bring up objective 3
	wait 0.5
	waitthread global/objectives.scr::add_objectives 3 2
	waitthread global/objectives.scr::current_objectives 3
	waitthread maps/e3l3/scene2.scr::DoObjective3  //scene 2

	// complete objective 3, bring up objective 4
	waitthread global/objectives.scr::add_objectives 3 3
	waitthread global/objectives.scr::add_objectives 4 2
	waitthread global/objectives.scr::current_objectives 4
	waitthread maps/e3l3/scene2.scr::DoObjective4  //scene 2
	waitthread maps/e3l3/scene2.scr::CleanupScene2

	// first railgun destroyed, modify objective 4
	waitthread global/objectives.scr::add_objectives 4 2 NIL $AB41.origin
	waitthread global/objectives.scr::current_objectives 4
	$steal_ab_trigger waittill trigger
	
	waitthread maps/e3l3/scene3.scr::DoObjective5   //scene 3

	// update compass location for objective 4
	waitthread global/objectives.scr::add_objectives 4 2 NIL $fakeobj1.origin
	waitthread global/objectives.scr::current_objectives 4
	$fakeobj1 waittill trigger

	// update compass location for objective 4
	waitthread global/objectives.scr::add_objectives 4 2 NIL $fakeobj2.origin
	waitthread global/objectives.scr::current_objectives 4
	$fakeobj2 waittill trigger

	// update compass location for objective 4
	waitthread global/objectives.scr::add_objectives 4 2 NIL $kfive_two.origin
	waitthread global/objectives.scr::current_objectives 4
	waitthread maps/e3l3/scene3.scr::DoObjective6  //ends scene 3
	waitthread maps/e3l3/scene3.scr::CleanupScene3
	wait 1.5

	//end level
	//AJS -- only use this for mission endings: e1l3, e2l3, e3l4
	//	missiontransition e3m3
	//Use this instead
	if ( level.playerjumped != 0 ){	
		// MJG: end mission successfully only if player jumped from the AB41 in time
		exec global/missioncomplete.scr e3l4 1
	}
	else {
		//missionfailed	//MiaCosa
		exec coop_mod/replace.scr::missionfailed
	}
}end

//=========================================================================
startMusic_e3l3_main: {
//=========================================================================
	thread global/music.scr::PlaySongLoop "sound/music/E3L3_Taking_Out_Railgun.mp3"
}end

//=========================================================================
Fadein:{
//=========================================================================
	level.startangles = $player.viewangles	//MiaCosa @chrissstrahl
	//$player physics_off	//MiaCosa
	exec coop_mod/replace.scr::physics_off
	exec global/fadein.scr
	waitframe

	waitthread global/fadein.scr::DoFadeIn e3l3_1 e3l3_2 e3l3_3 NIL 0
	$player.viewangles = level.startangles  //undo any mousemovment during the hud stuff
}end

//=========================================================================
Briefing: {
//=========================================================================
	if (level.skipbriefings == 0) {	//MiaCosa @chrissstrahl ändern?
		$player playsound DFRUK_E3L3_CM3805 wait	// "Baker, this is King 6.  We're getting the hell beat out of us at Anzio.  Recon has spotted a battery of German K5 Cannons near the railroad tracks in your area."
		$player waittill sounddone
		$player playsound DFRUK_E3L3_CM3806 wait	// "These tubes are tough nuts to crack.  You'll have to use demolition charges right in the muzzle to spike those guns."
		$player waittill sounddone
		$player playsound DFRUK_E3L3_CM3807 wait	// "Be advised we've got reports of a Jerry convoy carrying ammo headed your way."
		$player waittill sounddone
		$player playsound DFRUK_E3L3_CM3808 wait	// "Set up a demolition ambush and stop that convoy.  Keep yourself from being spotted at all costs."
		$player waittill sounddone
		//$player playsound DFRUK_E3L3_CM3809 wait	// "If they spot you up there, we're in real trouble.  If you run out of demo improvise."
		//$player waittill sounddone
	}

	//$player physics_on	//MiaCosa
	exec coop_mod/replace.scr::physics_on
	drawhud 2

	if (level.skipbriefings == 0){
		//$player playsound DFRUK_E3L3_CM3810 wait	// "King 6 out.  "	//MiaCosa @chrissstrahl
		exec coop_mod/replace.scr::playsound DFRUK_E3L3_CM3810 wait
$player loopsound …

…
$player loopsound …

		$player waittill sounddone	//MiaCosa @chrissstrahl
	}
}end

//=========================================================================
InitLevel: {
//=========================================================================
	level.script = "maps/e3l3.scr"
	//level.music  = ""

	thread startMusic_e3l3_main
	thread InitFogger
	thread InitFriends
	thread InitEnemies
	thread InitObjectives
	waitthread InitPlayer
	setcvar g_mission 3

	// added by ccaron
	// make the truck by the tunnel explode
	if ($tunneltruck == NULL){
		// println "Tunnel Truck entity unavailable!"
	}
	else{
	  for (local.i = 1; local.i <= $tunneltruck.size; local.i ++){
	    $tunneltruck[local.i].health = level.tunneltruckhealth
	    $tunneltruck[local.i] thread waitToExplode
	  }
	}

	level.frontdoorguystrigger nottriggerable
	exec global/autosave.scr 1	//"Starting"
}end

//=========================================================================
InitPlayer: {
//=========================================================================
	$player heal 1	//MiaCosa @chrissstrahl
	waitthread Fadein
	waitthread ArmPlayer
	waitthread Briefing
	waitthread GiveObjectives
}end

//=========================================================================
ArmPlayer: {
//=========================================================================
	waitexec global/loadout.scr
	waitthread global/items.scr::blank_items
	waitthread global/items.scr::add_item "radio"
	waitthread global/items.scr::add_item "explosives" noprint
	waitthread global/items.scr::add_item "plunger" noprint
}end

//=========================================================================
InitFriends: {
//=========================================================================
	//none of these
}end

//=========================================================================
InitEnemies: {
//=========================================================================
	for (local.i=1;local.i<$mg42.size+1;local.i++) {
		// level.spot_burstmin level.spot_burstmax level.spot_burstdownmin level.spot_burstdownmax	
		$mg42[local.i] burstfiresettings 1 3 1.5 3
		$mg42[local.i] maxyawoffset 60.0
		$mg42[local.i] pitchcaps "-20 20 0"
		$mg42[local.i] convergetime 0.25

		switch (level.skill){
			case 0:
				$mg42[local.i] aibulletspread 450 275
				break
			case 1:
				$mg42[local.i] aibulletspread 400 225
				break
			case 2:
				$mg42[local.i] aibulletspread 300 125
				break
		}
	}

	$tank.paused = 1
	$tank thread global/vehicles_thinkers.scr::enemy_tank_think2 0 bridge_panzer
	$flak thread global/vehicles_thinkers.scr::enemy_flak_think
	//$flak setcollisionentity $flak88_collision
	$flak setcollisionentity $flak88_turret_collision

	$mg42guy1  ai_off  //mg42 gunner near aa gun --a trigger turns on his ai
	$mg42guy1b ai_off  //mg42 gunner near aa gun --a trigger turns on his ai
	$mg42guy2  ai_off  //mg42 gunner near ab41 --a trigger turns on his ai
	$mg42guy3  ai_off  //mg42 gunner near ab41 --a trigger turns on his ai
}end

//=========================================================================
InitObjectives: {
//=========================================================================
	//nothing special
}end

//=========================================================================
GiveObjectives: {
//=========================================================================
	waitthread global/objectives.scr::add_objectives 2 2 "Destroy the Munitions Convoy" $dasbox.origin
	waitthread global/objectives.scr::add_objectives 3 1 "Search the House for Intelligence" $anziomap.origin
	waitthread global/objectives.scr::add_objectives 4 1 "Locate and Destroy K5 Railway Guns" $kfive_one_explosive1.origin
	waitthread global/objectives.scr::current_objectives 2
}end

//=========================================================================
SpawnK5Guys: {
//=========================================================================
	// give the spawner a chance to spawn
	wait 0.5

	//$k5jumper type_attack "turret"
	//$k5jumper exec global/setdeathanim.scr "death_headpistol"
	//$k5slider type_attack "turret"
	//$k5slider exec global/setdeathanim.scr "death_headpistol"

    // these animations are causing strange side-effects
	$k5jumper thread InitK5GuySimple
	//$k5slider thread FallerGuy
	$k5faller thread InitK5GuySimple
}end

//=========================================================================
InitK5GuySimple: {
//=========================================================================
	self exec global/setdeathanim.scr "death_headpistol"
  
	// this here to keep guy from moving around
	self type_attack "turret"
}end

//=========================================================================
InitDude: {
//=========================================================================
	thread DudeNotDumb
	thread DudeNotNumb
}end

//=========================================================================
dude_kicks: {
//=========================================================================
	// iprintln "dude kicks"
	if ($dude && level.dudehasenemy == 0){
		// iprintln "dude kicks2"
		$dude anim AA_AB41_working
		$dude waittill animdone
		wait 0.4

		$dude exec global/enable_ai.scr
	}
}end

//=========================================================================
DudeNotDumb: {
//=========================================================================
	level.dudehasenemy = 0
	$dude waittill hasenemy
	level.dudehasenemy = 1
	$dude exec global/enable_ai.scr
}end

//=========================================================================
DudeNotNumb: {
//=========================================================================
	$dude waittill pain
	$dude exec global/enable_ai.scr
}end

//=========================================================================
OpenRailDoor: {
//=========================================================================
	$dasdoor speed 256
	$dasdoor playsound metal_bunker_hatch_open
	wait .8

	$dasdoor playsound door_railcar_open_fast
	$dasdoor MoveSouth 116
	$dasdoor move

	for (local.i=1;local.i<$trainguys.size+1;local.i++){
		$trainguys[local.i] exec global/enable_ai.scr
	}
}end

//=========================================================================
CollapseTrees: {
//=========================================================================
	// iprintln "Trying to collapse a tree"
	$bridgetrees thread KillTree
}end

//=========================================================================
KillTree: {
//=========================================================================
	// iprintln "Collapsing a tree"
	self anim death
	self nodamage
}end

//=========================================================================
FindTrigger local.find_name: {
//=========================================================================
	for (local.i=1;local.i<$enemyspawnertrigger.size+1;local.i++) {
		if ($enemyspawnertrigger[local.i].find_name == local.find_name) {
			local.returntrigger = $enemyspawnertrigger[local.i]
			goto FindTriggerEnd
		}
	}

	for (local.i=1;local.i<$explodertrigger.size+1;local.i++) {
		if ($explodertrigger[local.i].find_name == local.find_name) {
			local.returntrigger = $explodertrigger[local.i]
			goto FindTriggerEnd
		}
	}

	for (local.i=1;local.i<$paradetrigger.size+1;local.i++) {
		if ($paradetrigger[local.i].find_name == local.find_name) {
			local.returntrigger = $paradetrigger[local.i]
			goto FindTriggerEnd
		}
	}

	for (local.i=1;local.i<$autotrucktrigger.size+1;local.i++) {
		if ($autotrucktrigger[local.i].find_name == local.find_name) {
			local.returntrigger = $autotrucktrigger[local.i]
			goto FindTriggerEnd
		}
	}

	for (local.i=1;local.i<$autoplanetrigger.size+1;local.i++) {
		if ($autoplanetrigger[local.i].find_name == local.find_name) {
			local.returntrigger = $autoplanetrigger[local.i]
			goto FindTriggerEnd
		}
	}
	
	for (local.i = 1; local.i < $autotanktrigger.size+1; local.i++) {
		if ($autotanktrigger[local.i].find_name == local.find_name) {
			local.returntrigger = $autotanktrigger[local.i]
			goto FindTriggerEnd
		}
	}

FindTriggerEnd:
}end local.returntrigger

//=========================================================================
FindTriggerByTarget local.target_name:
//=========================================================================
	for (local.i=1;local.i<$enemyspawnertrigger.size+1;local.i++){
		if ($($enemyspawnertrigger[local.i].target) == local.target_name){
			local.returntrigger = $enemyspawnertrigger[local.i]
			goto FindTriggerByTargetEnd
		}
	}

	FindTriggerByTargetEnd:
}end local.returntrigger

// Truck Exploding Gag
//=========================================================================
waitToExplode: {
//=========================================================================
	self anim idlenolights
	thread global/vehicles_thinkers.scr::truck_INIT

	//self immune grenade
	//self immune bullet
	//self immune fast_bullet
	//self immune explosion
	//self immune rocket

	self waittill death
	self.collisionent disconnect_paths
	thread global/vehicles_thinkers.scr::truck_killed self.type

	// Joel's ligering smoke method, NI yet
	self thread maps/e3l3/scene1.scr::TruckSmoke 
}end

// -----------------------------------------------
//
// Balcony Jump Gag
//
// by ccaron  4/25/2003
//
// a balcony guy is inactive until hit by a bullet
// or player becomes visible
//
// if the guy is killed on the balcony, a 
// balcony fall anim is executed.  If the 
// guy is alarmed, then one of 2 possible
// jump-over-the-railing anims is executed.
//
// call as: $guy thread balconyGuy
//
// -----------------------------------------------
//=========================================================================
balconyGuy:
//=========================================================================  
 	if(!(isalive self) || self.health <= 0) {
		end 
 	}

  	// need the set health very high to catch
  	// when guy is dead.
  	self.health += 10000
  	self.nolongpain = 1
  	self.ainocrouch = 1
  	self.ainoprone = 1
  	self.ainododge = 1
  	self.ainohopforward = 1
  	self.ainohopbackward = 1
  	self.ainointerval = 1
  	self.fixedleash = 1
  	self.mindist = 0
  	self.leash = 0
  	self.type_grenade = balcony_grenade
  
  	// this here to keep guy from moving around
  	self type_attack "turret"

  	// get the direction to fall
  	self.fallDir = angles_toforward(self.angles)
  	self.fallDir = vector_scale self.fallDir 100

  	// get the direction to jump
  	local.angle = self.angles
  	//self.jumpType = randomint(2)
  	self.jumpType = 0

  	if (self.jumpType == 0) {
		local.angle[2] -= 90
  	}
  
	self.jumpDir = angles_toforward(local.angle)  
	self.jumpDir = vector_scale self.jumpDir 100

	// wait to get hurt  
	self waittill pain
	self waitexec global/disable_ai.scr

	// if alive, jump off balcony and attack
	if (self.health > 10000) {
    	//println "balcony guy jumping off"
  		self thread TurnTo (self.jumpDir + self.origin)
	
  		if (self.jumpType == 0) {
			self anim_noclip AA_K5_jump
  		}
		else {
	   		self anim_noclip 13C500_Capthopsbalc
		}

	  	self waittill animdone
	  	//self physics_off
	  	self droptofloor
	  
	  	if (self.health > 10000) {
  	    	self.health -= 10000
	  	}
	  	else {
	    	//println "less than 10000!!!!!!!!"	
			//self bedead
			self.health = 1
			//end
	  	}

	  	self.type_attack = "cover"
	  	//self physics_on
	  	self exec global/enable_ai.scr
  	}
  	else { // perform a fall off the balcony anim
		//println "balcony guy dead"
		self nodamage
		self notsolid
		self exec global/disable_ai.scr
		self thread TurnTo (self.fallDir + self.origin)
		self anim_noclip death_balcony_intro
		self waittill animdone
		wait 0.4

		while (vector_length( self.velocity ) > 100) {
			self anim death_balcony_loop
			wait .1
		}

		self anim death_balcony_outtro
		self waittill animdone
		self bedead
	}
}end

//=========================================================================
TurnTo local.origin: {
//=========================================================================
	if (local.origin) {
		local.ent = spawn script_origin
		local.ent.origin = local.origin
		self turnto local.ent
		self waittill turndone
		local.ent remove
	}
	else {
		// iprintln "bad call!!!!!!!!!!"
	}
}end

//=========================================================================
BlowGuyFromTower: {
//========================================================================= 
	// iprintln "Blowin da guy from tower!"
	local.temp = spawn script_model model models/fx/fx_truck_explosion.tik
	local.temp.origin = self.origin
	waitframe

	local.temp anim start
	radiusdamage self.origin 4000 500
	self playsound explode_tower

	for(local.i = 1; local.i <= $faller.size; local.i++){
		if(vector_length($faller[local.i].origin - self.origin) < 200){
			$faller[local.i] exec global/setdeathanim.scr "death_mortar_flip"
			$faller[local.i] exec global/bullethit.scr (0 0 250) 5000 0 1
		}
	}

	wait 5
	local.temp remove
}end

// Use this thread to turn emmitters on and off
// when not visible by the player
//=========================================================================
EmmitterCheck: {
//=========================================================================
 	while (self && isalive self) {
		if ($player cansee self 120 4000) {	//MiaCosa @chrissstrahl
			self show
		}
		else {
	  		self hide
		}
		wait .1
  	}
}end

//Jump if hit
//=========================================================================
JumperGuy: {
//=========================================================================
	if(!(isalive self) || self.health <= 0) {
		end 
	}

	// need the set health very high to catch
	// when guy is dead.
	self.health += 1000
	self.nolongpain = 1
	self.ainocrouch = 1
	self.ainoprone = 1
	self.ainododge = 1
	self.ainohopforward = 1
	self.ainohopbackward = 1
	self.ainointerval = 1
	self.fixedleash = 1
	self.mindist = 0
	self.leash = 0
	self.type_grenade = balcony_grenade
	self exec global/setdeathanim.scr "death_headpistol"
  
	// this here to keep guy from moving around
	self type_attack "turret"

	// get the direction to fall
	self.fallDir = angles_toforward(self.angles)
	self.fallDir = vector_scale self.fallDir 100

	// get the direction to jump
	local.angle = self.angles
	//self.jumpType = randomint(2)
	self.jumpType = 0

	if (self.jumpType == 0){
		local.angle[2] -= 90
	}
  
	self.jumpDir = angles_toforward(local.angle)  
	self.jumpDir = vector_scale self.jumpDir 100

	// wait to get hurt  
	self waittill pain

	if ($kfive_one.shutdown == 0) {
		self exec global/bullethit.scr (0 0 20) 10000 0 1

		if ( isalive self ) {
			self waitexec global/disable_ai.scr

			// if alive, jump off balcony and attack
			//println "balcony guy jumping off"
			self thread TurnTo (self.jumpDir + self.origin)

			if (self.jumpType == 0) {
				self anim_noclip AA_K5_jump
			}

			self waittill animdone
			self droptofloor
			self.type_attack = "cover"
			self exec global/enable_ai.scr
		}
	}
end

//Die and fall if hit
//=========================================================================
FallerGuy: {
//=========================================================================
  	if(!(isalive self) || self.health <= 0) {
		end 
  	}

  	// need the set health very high to catch
  	// when guy is dead.
  	self.health += 10000
  	self.nolongpain = 1
  	self.ainocrouch = 1
  	self.ainoprone = 1
  	self.ainododge = 1
  	self.ainohopforward = 1
  	self.ainohopbackward = 1
  	self.ainointerval = 1
  	self.fixedleash = 1
  	self.mindist = 0
  	self.leash = 0
  	self.type_grenade = balcony_grenade
  
  	// this here to keep guy from moving around
  	self type_attack "turret"

  	// get the direction to fall
  	self.fallDir = angles_toforward(self.angles)
  	self.fallDir = vector_scale self.fallDir 100

  	// get the direction to jump
  	local.angle = self.angles
  
  	self.jumpDir = angles_toforward(local.angle)  
  	self.jumpDir = vector_scale self.jumpDir 100

  	// wait to get hurt  
  	self waittill pain

  	if ($kfive_one.shutdown == 0) {
	  	self waitexec global/disable_ai.scr

		//println "balcony guy dead"
		self nodamage
		self notsolid
		self exec global/disable_ai.scr
		self thread TurnTo (self.fallDir + self.origin)
		self anim_noclip death_balcony_intro
		self waittill animdone
		wait 0.4

		while (vector_length( self.velocity ) > 100) {
			self anim death_balcony_loop
			wait .1
		}

		self anim death_balcony_outtro
		self waittill animdone
		self bedead
	}
}end

//=========================================================================
InitFogger: {
//=========================================================================
	level.k4fogplane = 3100
	level.farclipoverride = 3100
	level.defaultfogplane = $world.farplane
	level.fogbias = $world.farplane_bias
	level.fogcolor = $world.farplane_color
	level.fogging = 0
}end