// BattleHouse

//============================================================================
Init: {
//============================================================================
	$doorBattleHouse lock
		
	//do initialization for battle between the houses
	$battleHouseGunner_Front.health = 200
	$battleHouseGunner_Front ai_off
	$battleHouseGunner_Front nodamage
	$battleAirborne_Trooper nodamage
	$battleAirborne_Commander nodamage
	$battleAirborne_Commander ai_off
	$battleAirborne_Trooper ai_off

	if (level.skipbattlehouse == 1) {
	
		
		//[204] chrissstrahl
		if(!level.skiptown){
			for(local.i=1;local.i<9;local.i++){
				level.flags["coop_spawn"+local.i+"origin"] = ( -3934 3651 -230 )
				level.flags["coop_spawn"+local.i+"angles"] = ( 0 1 0 )	
			}
			if($player){
				for (local.i = 1;local.i <= $player.size;local.i++){
					local.player =  $player[local.i]
					if( local.player.health > 0){
						local.player.origin = ( -3934 3651 -230 )
					}
				}
			}
		}
		
		$battleHouseGunner_Front delete
		$battleHouseGunner_Rear delete
		waitthread global/ObjMgr.scr::CompleteObj "meet82nd"
		waitthread global/ObjMgr.scr::RevealObj "assist82nd"
		waitthread global/ObjMgr.scr::SetObjCompassTarg "assist82nd" $compass_point_to_town
		end
	}

	$stopHouseParade nottriggerable
	thread doWork
}end	

//============================================================================
doWork: {
//============================================================================
	$battleTrigger waittill trigger
	
	//start up the battle between the guys
	thread activateAxisHouseGuys
	thread activateAllyHouseGuys
	
	//do anims and update objectives when the player nears the 82nd commander
	thread findTheAllies
	thread stopHouseParades
	thread initMGs
}end

//============================================================================
initMGs: {
//============================================================================
	for (local.i=1;local.i<$mg42.size+1;local.i++)  { 
		// level.spot_burstmin level.spot_burstmax level.spot_burstdownmin level.spot_burstdownmax
		$mg42[local.i] burstfiresettings 1 3 1.5 3 
		$mg42[local.i] maxyawoffset 60.0 
		$mg42[local.i] pitchcaps "-20 20 0" 
		$mg42[local.i] convergetime 0.25 
  
		switch (level.skill)  { 
			case 0: 
			     $mg42[local.i] aibulletspread 450 275 
			     break 
	
			case 1: 
			     $mg42[local.i] aibulletspread 400 225 
			     break
	
			case 2: 
			     $mg42[local.i] aibulletspread 300 125 
			     break 
		} 
	} 

	$battleHouseBreda_Front burstfiresettings 1 3 1.5 3 
	$battleHouseBreda_Front maxyawoffset 60.0 
	$battleHouseBreda_Front pitchcaps "-20 20 0" 
	$battleHouseBreda_Front convergetime 0.25 

	switch (level.skill)  { 
		case 0: 
		     $battleHouseBreda_Front aibulletspread 450 275 
		     break 

		case 1: 
		     $battleHouseBreda_Front aibulletspread 400 225 
		     break

		case 2: 
		     $battleHouseBreda_Front aibulletspread 300 125 
		     break 
	} 
}end

//============================================================================
findTheAllies: {
//============================================================================
	while (1) {
		waitframe

		//[204] chrissstrahl
		if(level.gametype == 0){
			if (vector_length ($player.origin - ($battleAirborne_Commander).origin) > 100) {
				continue
			}

			if (  sighttrace ( $player.origin + ( 0 0 50 ) ) ( $battleAirborne_Commander.origin + ( 0 0 50 ) ) 1 ) {
				break
			}
		}else{
			if($player){
				if(waitthread coop_mod/replace.scr::playerCansee $battleAirborne_Commander 60 150){
					break
				}
				waitframe
			}
		}
	}

	$battleAirborne_Commander thread DoDialog	
	$stopHouseParade triggerable
	
	//update objectives
	waitthread global/ObjMgr.scr::CompleteObj "meet82nd"
	waitthread global/ObjMgr.scr::RevealObj "assist82nd"
	waitthread global/ObjMgr.scr::SetObjCompassTarg "assist82nd" $summonPlane

	//Stop the machine gun parade
	local.trigger = waitthread global/FindTrigger.scr::FindTrigger "pmBattleHouseMG_AbortTrigger"
	trigger local.trigger
	$battleHouseBreda_Front.paradeStopped = 1
	
	//guys come out the door of the other house
	thread doorGuysComeOut
	
	$battleAirborne_Commander thread followPlayer
	$battleAirborne_Trooper thread followPlayer
}end

//============================================================================
DoDialog: {
//============================================================================
	//do any dialogue, animation, etc here
	self exec global/disable_ai.scr
	
	//[204] chrissstrahl
	self waitthread coop_mod/replace.scr::turntolookat
	self waitthread coop_mod/replace.scr::runtoClosest


	self waittill movedone
	self upperanim A_23_Hudson_1A
	self waittill upperanimdone
	wait 0.7

	self upperanim A_23_Hudson_1B
	self waittill upperanimdone
	wait 0.7

	self upperanim A_23_Hudson_1C
	self waittill upperanimdone
	wait 1.0

	self exec global/enable_ai.scr

	//commander can be damaged safely now
	self turnto NULL
	self lookat NULL
	self runto NULL
	self takedamage
}end

//============================================================================
battleHouseMGUse: {
//============================================================================
	local.trigger = waitthread global/FindTrigger.scr::FindTrigger "pmBattleHouseRearMG_Trigger"
	trigger local.trigger
	level.stopFakeFire = 1
}end

//============================================================================
doorGuysComeOut: {
//============================================================================
	$doorBattleHouse unlock
	local.trigger = waitthread global/FindTrigger.scr::FindTrigger "battleHouseSpawner2"
	//[204] chrissstrahl
	if(!local.trigger){
		trigger local.trigger
	}
}end

//============================================================================
activateAxisHouseGuys: {
//============================================================================
	$battleHouseGunner_Front ai_on
	$battleHouseGunner_Front takedamage
	$battleHouseBreda_Front thread global/mg42init.scr::AttachGuyToMG42 $battleHouseGunner_Front   
	
	local.trigger = waitthread global/FindTrigger.scr::FindTrigger "battleHouseSpawner1"
	trigger local.trigger

	//start the window parades
	local.trigger = waitthread global/FindTrigger.scr::FindTrigger "pmBattleHouseWindow1_Trigger"
	trigger local.trigger
	local.trigger = waitthread global/FindTrigger.scr::FindTrigger "pmBattleHouseWindow2_Trigger"
	trigger local.trigger
	local.trigger = waitthread global/FindTrigger.scr::FindTrigger "pmBattleHouseWindow3_Trigger"
	trigger local.trigger

	//start upstairs main MG parade
	local.trigger = waitthread global/FindTrigger.scr::FindTrigger "pmBattleHouseMG_Trigger"
	trigger local.trigger

	$battleHouseBreda_Front thread assignMGGunners
}end

//============================================================================
assignMGGunners: {
//============================================================================
	self.target = "pmBattleHouseMG_paradeguy"

	while (self != NULL) {
		while ($pmBattleHouseMG_paradeguy == NIL || $pmBattleHouseMG_paradeguy == NULL || ( ! ( isalive $pmBattleHouseMG_paradeguy ) ) ) {
			wait 0.1
		}

		$pmBattleHouseMG_paradeguy exec global/disable_ai.scr
		$pmBattleHouseMG_paradeguy.origin = ( -4234 4217 -72 )

		self waitthread AttachGuyToMG42 $pmBattleHouseMG_paradeguy
		$pmBattleHouseMG_paradeguy exec global/enable_ai.scr

		while ($pmBattleHouseMG_paradeguy != NULL && (isalive $pmBattleHouseMG_paradeguy)) {
			wait 0.1
		}

		if ( ! (isalive $pmBattleHouseMG_paradeguy) ) {
			if (self.paradeStopped != 1) {
				$pmBattleHouseMG_paradeguy remove
			}
		}
	}
}end

//============================================================================
AttachGuyToMG42 local.guy local.movedoneradius local.auto_reattach: {
//============================================================================
	if (!isalive local.guy) {
		end
	}

	if (!local.movedoneradius) {
		local.movedoneradius = 150
	}

	if (!local.auto_reattach) {
		local.auto_reattach = 0
	}

	local.runtospot = spawn script_model model "fx/dummy.tik"
	local.runtospot.origin = ( -3791 4320 -73 )
	wait 2.0

	local.guy runto local.runtospot
	local.guy avoidplayer 0

	while(local.guy && vector_length(local.guy.origin - self.origin) > local.movedoneradius) {
		waitframe
		local.guy runto local.runtospot
	}
	
	if (!local.guy || !(isalive local.guy)) {
		end
	}

	self.target = local.guy
	self thread global/mg42init.scr::mg42_start NIL local.auto_reattach

	local.guy physics_off
	local.guy.ainocrouch = 1
	local.guy.ainoprone = 1
}End

//============================================================================
activateAllyHouseGuys: {
//============================================================================
	$battleAirborne_Trooper takedamage
	$battleAirborne_Trooper ai_on
	$battleAirborne_Trooper exec global/enable_ai.scr
	$battleAirborne_Commander ai_on
	$battleAirborne_Commander exec global/enable_ai.scr
}end

//============================================================================
followPlayer: {
//============================================================================ 
	self.distance = 150 + randomint 100
	
	//[204] chrissstrahl
	self waitthread coop_mod/main.scr::destination
	
	
	self.friendtype = 1
	self.avoidplayer = 1
	self thread global/friendly.scr::friendlythink
}end

//============================================================================
stopHouseParades: {
//============================================================================
	local.trigger = waitthread global/FindTrigger.scr::FindTrigger "pmBattleHouseMG_Trigger"
	local.originbase = local.trigger.origin

	$stopHouseParade waittill trigger

	local.trigger = waitthread global/FindTrigger.scr::FindTrigger "pmBattleHouseWindow1_AbortTrigger"
	trigger local.trigger
	local.trigger = waitthread global/FindTrigger.scr::FindTrigger "pmBattleHouseWindow2_AbortTrigger"
	trigger local.trigger
	local.trigger = waitthread global/FindTrigger.scr::FindTrigger "pmBattleHouseWindow3_AbortTrigger"
	trigger local.trigger

	for (local.i = 1; local.i < $t229234578.size; local.i++) {
		thread tryFakeFile $t229234578[local.i].origin local.originbase
	}

	$summonPlane waittill trigger
	level.stopFakeFire = 1
}end

//============================================================================
tryFakeFile local.origin local.otherorigin: {
//============================================================================
	// There are some bogus entries, only use those right infront of the window
	local.vect1 = vector_normalize ( $battleHouseRearMGTarget.origin - local.origin )
	local.vect2 = vector_normalize ( local.otherorigin - local.origin )
	local.dot = vector_dot local.vect1 local.vect2

	if (local.dot < .93) {
		end
	}

	// Wait until the MG gunner has died before starting up the gunfire
	while ($battleHouseGunner_Rear && $battleHouseGunner_Rear.health > 0) {
		wait 0.1
	}

	local.pg = spawn ProjectileGenerator_Gun origin local.origin
	local.pg.model = "statweapons/p_aagun_cannon.tik"
	local.pg thread startFakeFire
}end

//============================================================================
startFakeFire: {
//============================================================================
	self ID 1
	$battleHouseRearMGTarget ID 1
	self.mytarget = $battleHouseRearMGTarget

	wait ( 0.5 + randomfloat(12))

	self turnoff
	self tracerspeed 1.0
	self bulletspread 2 2 30 30
	self bulletdamage 1
	self Accuracy 5
	self MaxDelay 0
	self MinDelay 0
	self MaxNumShots 1
	self MinNumShots 2
	self bulletlarge 1
	self tracerspeed 1
	self target $battleHouseRearMGTarget

	while (level.stopFakeFire != 1) {
		self turnon
		wait ( 0.5 + randomfloat(10))
		self turnoff
		wait ( 0.5 + randomfloat(1))
	}
}end