// thinkTank.scr

// most of this code was copied from vehicles_thinkers so that I could 
//    customize it for e1l1 (get it to not use global variables, so that
//    multiple tanks can have multiple targets, etc).
//============================================================================
tankGo: {
//============================================================================
	// essential parameters that are not set get defaults	
	if (self.health == NIL) {
		self.health = 400
	}

	self.health_set = 1

	if (self.tankSpeed == NIL) {
		self.tankSpeed = 200
	}

	if (self.tankLookAhead == NIL) {
		self.tankLookAhead = 200
	}

	if (self.tankPathLoop == NIL) {
		self.tankPathLoop = 0
	}

	if (self.tankAccel == NIL) {
		self.tankAccel = 30
	}

	if (self.tankDriveDelay == NIL) {
		self.tankDriveDelay = 0 
	}

	if (self.tankEngageDelay == NIL) {
		self.tankEngageDelay = 0
	}

	if (self.tankStunTime == NIL) {
		self.tankStunTime = 5
	}

	if (self.tankStopIfClose == NIL) {
		self.tankStopIfClose = 0
	}

	if (self.tankGuysJumpOut == NIL) {
		self.tankGuysJumpOut = 0
	}

	if (self.tankReloadTime == NIL) {
		self.tankReloadTime = 5
	}

	if (self.tankDisableDriveEngage == NIL) {
		self.tankDisableDriveEngage = 0
	}

	if (self.tankDontActuallyFire == NIL) {
		self.tankDontActuallyFire = 0
	}

	if (self.tankTossOnDeath == NIL) {
		self.tankTossOnDeath = 0
	}

	if (self.tankShotLimit == NIL) {
		self.tankShotLimit = -1		// no shot limit
	}

	if (self.tankDisableDriveEngage == 1) {
		self.tankPathEnd = 0
	}
	else {
		self.tankPathEnd = 1
	}

	// maybe we want to see the debugging info
	if (level.tankDebugPrint) {
		self waitthread tankPropertiesPrint
	}

	// start basic thinking loops
	if (self.tankTarget == NULL) {
		local.notattacking = 1
	}
	else {
		local.notattacking = 0
	}

	self thread tankEngageEnemy local.notattacking 
	self thread tankDrivePath 

	// if the player gets too close, then they die!
	if (self.tankKillPlayer) {
		self thread tankKillPlayer
	}
}end

//============================================================================
tankEngageEnemy local.nottacking: {
//============================================================================
	//if (self.tankTarget == NIL || self.tankTarget == NULL)
	//	end

	if (self.tankEngageDelay > 0) {
		if (level.tankDebugPrint) {
			dprintln self.targetname " engage delaying " self.tankDriveDelay
		}
		wait self.tankEngageDelay
	}

	if (level.tankDebugPrint) {
		dprintln self.targetname " engaging "
	}
	self thread enemy_tank_think local.notattacking self.tankType
}end

//============================================================================
tankDrivePath: {
//============================================================================
	if (self.tankPath == NIL || self.tankPath == NULL) {
		if (level.tankDebugPrint) {
			dprintln self.targetname " no path, not driving"
		}
		end
	}

	// drive, if need be
	if (self.tankDriveDelay > 0) {
		if (level.tankDebugPrint) {
			dprintln self.targetname " drive delaying " self.tankDriveDelay
		}
		wait self.tankDriveDelay
	}

	if (level.tankDebugPrint) {
		dprintln self.targetname " moving out"
	}

	while (self != NIL && self != NULL) {
		self.driving = 1
		self drive self.tankPath self.tankSpeed self.tankAccel 200 self.tankLookAhead
		self waittill drive
		self.tankPathEnd = 1

		if (self) {
			self stop
			self.driving = 0
		}

		if (!self.tankPathLoop) {
			break
		}
		wait 1
	}
}end

//============================================================================
tankPropertiesPrint: {
//============================================================================
	// belch all the properties out to the console
	dprintln "Tank " self.targetname " ---------------------------------"
	dprintln "  health = " self.health

	if (self.tankTarget == NULL || self.tankTarget == NIL) {
		dprintln "  tankTarget = (none)"
	}
	else {
		dprintln "  tankTarget = " self.tankTarget.targetname
	}

	dprintln "  tankType = " self.tankType
	dprintln "  speed = " self.tankSpeed
	dprintln "  accel = " self.tankAccel
	dprintln "  lookahead = " self.tankLookAhead
	
	if (self.tankPath == NULL) {
		dprintln "  path = (none)"
	}
	else {
		dprintln "  path = " self.tankPath.targetname
	}

	dprintln "  drive delay = " self.tankDriveDelay
	dprintln "  engage delay = " self.tankEngageDelay
	dprintln "  stun time = " self.tankStunTime
	dprintln "  stop if close to player = " self.tankStopIfClose
	dprintln "-------------------------------------------------------"
}end

//============================================================================
enemy_tank_think local.notattacking local.type: {
//============================================================================
	self.type = local.type
	self rendereffects "-shadow"

	// make sure we only set health once...
	if ( self.health_set==NIL || self.health_set==0 ) {
		self.health_set = 1
		self.start_health = (((randomint 3) + 1) * 400)
		self.health = self.start_health
	}

	self removeondeath 0
	level.tank_stunned_time = 80

	if (level.tankDebugPrint) {
		dprintln "z:          self.target " self.target 
	}

	if (self.target) {
		self.collisionent = self.target
		//	self.collisionent hide
	}

	self.gun = self QueryTurretSlotEntity 0
	self.gun2 = self QueryTurretSlotEntity 1
	if (self.gun) {
		self.gun nodamage
	}

	if (self.gun2){
		self.gun2 nodamage
	}

	if (level.sightdistance == NIL) {
		level.sightdistance = 16000
	}

	if (self.death_thread==NULL || self.death_thread==NIL) {
		self thread TankExplodeOnDeath local.type
	}

	// wait until we have a target
	while (self != NULL && self != NIL && (IsAlive self) && (self.tankTarget == NULL || self.tankTarget == NIL)) {
		wait 1
	}

	if (self == NULL || self == NIL || !(IsAlive self)) {
		//if (self == NULL || self == NIL || !(IsAlive self) || self.tankTarget == NULL || self.tankTarget == NIL )
		end
	}

	if (local.notattacking != 1 && self.tankTarget != NULL && self.tankTarget != NIL && self.tankPathEnd == 1) {
		//while ( (level.playertanktarget == 0) || (level.playertanktarget == NIL) )
		//	wait 1
		self waitthread resetAimTarget

		if (self.tankDontActuallyFire == 0) {
			thread enemy_tank_attack_loop self.gun
			self.attack_thread = parm.previousthread
		}
		thread tank_pain self.gun
		self.pain_thread = parm.previousthread
	}

	if (self.tankStopIfClose) {
		thread tank_stop_if_close
	}
}end

//============================================================================
TankExplodeOnDeath local.type: {
//============================================================================
	self.death_thread	= local
	self removeondeath 0
	self.type = local.type
	self rendereffects "-shadow"

	if ( self.health_set==NIL || self.health_set==0 ) {
		self.health_set = 1
		self.start_health = (((randomint 3) + 1) * 400)
		self.health = self.start_health
	}

	self removeondeath 0
	self waittill death

	if (level.tankDebugPrint) {
		dprintln "z:         " self " is dead. self.start_health: " self.start_health
	}

	self thread tank_killed
}end

//============================================================================
tank_stop_if_close: {
//============================================================================
	//[204] chrissstrahl
	while ( (isAlive self) && !(waitthread coop_mod/replace.scr::withinDistanceOf self 450) ) {
		wait 1
		//dprintln "z:         tank stop if close: " self " is not close"	
	}

	if !(isAlive self) {
		end
	}

	if (self.driving == 1) {
		self stop
	}

	//dprintln "z:         tank stop if close: " self " is close. stopping."
}end

//============================================================================
tank_killed: {
//============================================================================
	local.tankGuysJumpOut = self.tankGuysJumpOut
	self playsound explode_tank
	self stop
	self.driving = 0

	local.do_collision = 1
	local.turretmodel = NIL
	local.type = self.type

	if (self.attack_thread) {
		self.attack_thread delete
	}

	if (self.pain_thread) {
		self.pain_thread delete
	}

	if (level.tankDebugPrint) {
		dprintln "z:          self.type: " self.type
	}

	switch (self.type) {
		case empty_turretless_tiger:
			local.model = models/vehicles/tigertank_wot_d.tik
			break

		case panzer_desert:
			local.model = models/vehicles/panzer_iv_d.tik
			break

		case panzer:
			local.model = models/vehicles/panzer_iv_eud.tik
			
			if (local.tankGuysJumpOut > 0) {
				// a guy jumps out!
				local.get_out = 1
				local.anims[0] = panzer_exit_1
				local.anims[1] = panzer_exit_2
				local.anims[2] = panzer_exit_3
			}
			else {
				// tank is on fire, everybody dies!
				thread maps/e1l1/explosion.scr::timedFireAndSmoke (self.origin + ( 0 0 100 )) 
			}
			break

		case empty_panzer_desert:
			local.model = models/vehicles/Panzer_destroyed_lowpoly_base.tik
			local.turretmodel = models/vehicles/Panzer_destroyed_lowpoly_cannon.tik
			local.get_out = 0
			break
		
		case sherman:
			thread maps/e1l1/explosion.scr::timedFireAndSmoke (self.origin + ( 0 0 100 )) 
			local.model = models/vehicles/shermantank_damaged.tik
			local.turretmodel = NIL
			local.get_out = 0
//			local.do_collision = 0
			break;
	
		case empty_tiger:
			thread maps/e1l1/explosion.scr::timedFireAndSmoke (self.origin + ( 0 0 100 )) 
			local.model = models/vehicles/tigertank_destroyed_base.tik
			local.turretmodel = models/vehicles/tigertank_destroyed_cannon.tik
			local.get_out = 0
//			local.do_collision = 0
			break;
		
		case bridge_tiger:
			// tiger tank that get's pushed into the bridge...
			local.model = models/vehicles/tigertank_destroyed_base.tik
			local.get_out = 0
			local.do_collision = 0
			break
		
		case panzerwerfer:
			local.model = models/vehicles/Panzerwerfer_d_base.tik
			local.turretmodel = models/vehicles/Panzerwerfer_d_cannon.tik
			local.get_out = 0
			break;
		
		case empty_panzer_winter:
			local.model = models/vehicles/PanzerIV_w_d_base.tik
			local.turretmodel = models/vehicles/PanzerIV_w_d_cannon.tik
			local.get_out = 0
			break;
		
		case panzer_winter:
			local.model = models/vehicles/PanzerIV_w_d_base.tik
			local.turretmodel = models/vehicles/PanzerIV_w_d_cannon.tik
			local.get_out = 1
			local.anims[0] = panzer_exit_1
			local.anims[1] = panzer_exit_2
			local.anims[2] = panzer_exit_3
			break;
		
		case empty_panzer_lowpoly:
			local.model = models/vehicles/panzer_destroyed_lowpoly_base.tik
			local.turretmodel = models/vehicles/panzer_destroyed_lowpoly_cannon.tik
			local.get_out = 0
//			local.do_collision = 0
			break;
		
		case empty_t34:
			local.model = models/vehicles/t34_base_d.tik
			local.turretmodel = models/vehicles/t34_cannon_d.tik
			local.get_out = 0
//			local.do_collision = 0
			break;
		
		case bridge_panzer:
			// tiger tank that get's pushed into the bridge...
			local.model = models/vehicles/Panzer_destroyed_lowpoly_base.tik
			local.get_out = 0
			local.do_collision = 0
			break
		
		case berlin_panzer:
			local.model = models/vehicles/panzer_destroyed_lowpoly_base.tik
			local.turretmodel = models/vehicles/panzer_destroyed_lowpoly_cannon.tik
			local.get_out = 1
			local.anims[0] = tank_exit_1
			local.anims[1] = tank_exit_2
			local.anims[2] = tank_exit_3
			local.anims[3] = tank_exit_4
			break
		case empty_carroP40:

			local.model = models/vehicles/It_V_CarroP40_Des.tik
			local.get_out = 0
			break
		
		default:
			local.model = models/vehicles/tigertank_destroyed_base.tik
			local.turretmodel = models/vehicles/tigertank_destroyed_cannon.tik
			local.get_out = 1
			local.anims[0] = tank_exit_1
			local.anims[1] = tank_exit_2
			local.anims[2] = tank_exit_3
			local.anims[3] = tank_exit_4
			break
	}

	local.name = self
	local.angles = self.angles
	local.origin = self.origin

	switch (self.type) {
		case empty_carroP40:
			local.brushmodel = $p40_des_hull.brushmodel
			break

		default:
			if (self.collisionent == NULL) {
				dprintln "!!! tank " self.targetname " has no collision entity"
			}
			else {
				local.brushmodel = self.collisionent.brushmodel
			}
			break
	}
	
	local.targetname = self.targetname

	//stop the smoke emitter that came from damage and start a temp emitter for the death model
	if (self.smoke) {
		self.smoke anim stop
		self.smoke = NIL
	}

	self anim temp_smoke
	self anim temp_sizzle
	
	self.gun = self QueryTurretSlotEntity 0
	self.gun2 = self QueryTurretSlotEntity 1

	if (self.gun) {
		local.turretangles = self.gun.angles
	}

	self thread spawn_fx models/emitters/explosion_tank.tik
	waitframe

	exec global/earthquake.scr .2 4 0 0
	//radiusdamage self.origin level.bomb_damage level.bomb_explosion_radius
	radiusdamage (self.origin + (0 0 200)) level.bomb_damage level.bomb_explosion_radius
	waitframe

	if (self.gun) {
		self DetachTurretSlot 0
		self.gun remove
	}

	if (self.gun2) {
		self DetachTurretSlot 1
		self.gun2 remove
	}

	self remove

	if ( local.do_collision==1 ) {
		local.collision = thread damaged_collision local.angles local.origin local.brushmodel
	}
	
	local.damaged = waitthread spawn_damaged_new local.model local.angles local.origin
	local.damaged.targetname = local.targetname
	local.damaged.smoke	= local.smoke

	//produce temporary smoke and burning sound
	local.damaged anim temp_smoke
	local.damaged anim temp_sizzle

	if ( local.collision != NIL ) {
		local.damaged notsolid
		local.collision disconnect_paths
	}

	if ( local.turretmodel != NIL ) {
		local.turretname = local.targetname + "_damaged_turret"
		
		if (self.tankTossOnDeath == 0) {
			local.damaged attachmodel local.turretmodel "turret0" 1 local.turretname
		}
		local.turretname detach
		local.turretname.angles = local.turretangles

		if (local.get_out == 1) {
			local.turretname thread tank_guys_get_out local.tankGuysJumpOut local.anims local.name local.type local.angles
		}
	}
	else {
		if (local.get_out == 1) {
			local.damaged thread tank_guys_get_out local.tankGuysJumpOut local.anims local.name local.type local.angles local.turretname
		}
	}

	if (self.tankTossOnDeath == 1) {
		local.damaged notsolid
		local.damaged thread toss self.velocity 1			// self.tossRefObj
//		local.turretname thread toss self.velocity 1
	}
}end

//============================================================================
toss local.baseVel local.besolid local.tossRefObj: {
//============================================================================
	local.v = local.baseVel
	local.v = (vector_scale local.v 0.1)

/*
	if (local.tossRefObj != NULL && local.tossRefObj != NIL) {
		if ((isalive local.tossRefObj)) {
			idprintlnbold "tossing away from " local.tossRefObj.targetname
			local.dir = (vector_normalize local.baseVel)

			local.toRef = local.tossRefObj.origin
			local.toRef -= self.origin
			local.toRef = (vector_normalize local.toRef)
			local.dot = (vector_dot local.dir local.toRef)
			//idprintlnbold "dir = " local.dir ", toRef = " local.toRef ", dot = " local.dot
			if (local.dot < -0.707) {
				// toss to the side
				local.v0 = ( local.v[0] local.v[1] 0 )
				local.v1 = ( 0 0 1)
				local.u = (vector_cross local.v0 local.v1)
				local.u = (vector_normalize local.u)
				//idprintlnbold "tossing to the side, u = " local.u
				if ((randomint 100) < 50)
					local.u = (vector_scale local.u 100)
				else
					local.u = (vector_scale local.u -100)
				local.v += local.u
			}
		}
		else {
			//idprintlnbold "not tossing to the side"
		}
	}
*/

	local.v += ( 0 0 250 )
	local.a = ( 0 0 -150 )
	local.startTime = level.time

	if ((randomint 100) < 50) {
		local.av = ( 5 0 12 )			//( 50 0 25 )
	}
	else {
		local.av = ( 5 0 -12 )			//( 50 0 -25 )
	}

	local.lastTime = level.time - 0.1
	while (level.time - local.startTime < 1.7) {
		local.slice = level.time - local.lastTime
		local.slice = local.slice*2.0
		local.lastTime = level.time

		local.dp = (vector_scale local.v local.slice)
		self.origin += local.dp
		local.dv = (vector_scale local.a local.slice)

		local.v += local.dv
		local.v[0] = local.v[0]*0.98
		local.v[1] = local.v[1]*0.98

		local.da = (vector_scale local.av local.slice)
		self.angles += local.da
		waitframe
	}		

//	if (self.isGuy == 1) {
//		local.a = self.angles
//		local.a[0] = 0
//		local.a[2] = 0
//		self.angles = local.a
//	}

	if (local.besolid) {
		self physics_on
		self solid
	}
}end

//============================================================================
tank_guys_get_out local.count local.anims local.name local.type local.angles local.turretname: {
//============================================================================
	wait 3

	if (self.state != "open") {
		if(local.turretname) {
			local.turretname anim open
		}
		else {
			self anim open
		}
		self playsound tank_snd_dooropen
	}
	
	wait 1
	//self waittill animdone

	if (self.state != "open") {
		if(local.turretname){
			local.turretname anim open_idle
		}
		else{
			self anim open_idle
		}
		self.state = "open"
	}

	//insert roberts random sorter here
	exec global/randomorder.scr local.anims

	//waitthread tank_guy_spawn local.anims[local.i] local.name local.type local.angles
	for (local.i = 0; local.i < local.count; local.i ++) {
		if (!local.anims[local.i])  {
			local.anims[local.i] = panzer_exit_3
		}
		waitthread tank_guy_spawn local.anims[local.i] local.name local.type local.angles
	}
}end

//============================================================================
tank_guy_spawn local.anim local.name local.type local.angles: {
//============================================================================
	if (level.tankDebugPrint) {
		dprintln "z:       spawning guy to get out " local.anim
	}

	switch ( local.type ) {
		case	panzer_winter:
			local.model_type = human/german_winter_Artillery-Crew
		break;

		default:
		local.temp = randomint 3
		
		// DHS: only tank commanders for now
		//if (local.temp == 0)
		//	local.model_type = human/german_panzer_grenadier
		//if (local.temp == 1)
		//	local.model_type = human/german_panzer_obershutze
		//if (local.temp == 2)
		local.model_type = human/german_panzer_tankcommander
		break;
	}

	local.ent = spawn local.model_type
	local.ent.origin = self gettagposition "tag_start"

	if ( local.angles!=NIL ) {
		local.ent.angles = local.angles
	}
	else {
		local.ent.angles = self gettagangles "tag_start"
	}

	local.ent noticescale 1
	local.ent sight 4500
	local.ent mindist 2024
	local.ent maxdist 2448
	local.ent leash 10000

	if (level.dontdropweapons == 1) {
		local.ent dontdropweapons
	}
	
	local.ent exec global/disable_ai.scr
	local.ent notsolid

	if (level.tankDebugPrint) {
		dprintln "z:         starting anim: " local.name " " local.anim
	}

	local.ent anim_noclip local.anim
	local.ent waittill animdone

	if (level.tankDebugPrint) {
		dprintln "z:         ending   anim: " local.name " " local.anim
	}

	if ( (local.anim == "tank_exit_2") || (local.anim == "panzer_exit_2") ) {
		local.ent.position = "crawl"
	}

	if ( (local.anim == "tank_exit_3") || (local.anim == "panzer_exit_3") ) {
		local.ent.health = 45 
	}

	if (local.anim == "tank_exit_4") {
		local.ent bedead
		end
	}

	wait .4
	if (level.tankDebugPrint) {
		dprintln "z:         making solid: " local.name " " local.anim
	}

	local.ent solid

	if (level.tankDebugPrint) {
		dprintln "z:         enabling AI: " local.name " " local.anim
	}

	local.ent exec global/enable_ai.scr
	local.ent attackplayer
}end

//============================================================================
tank_pain local.gun local.relock_on_player: {
//============================================================================
	local.previoushealth = self.health
	local.timer = 0
	self immune bash

	if ( local.relock_on_player==NIL ) {
		local.relock_on_player	= 1
	}

	local.timeToUnStun = 0

	while(isAlive self) {
		wait .1

		//if ( (isAlive self) && (self.health < local.previoushealth) )
		local.dHealth = local.previoushealth - self.health
		if ( (isAlive self) && local.dHealth > 100 && self.tankStunTime > 0) {
			if (level.tankDebugPrint) {
				dprintln "z:         " self " is damaged. self.start_health: " self.start_health
				dprintln "z:         current health: " self.health
				dprintln "z:         previous health: " local.previoushealth
				dprintln "z:         " self " is stunned"
			}

			self playsound damage_tank
			self.stunned = 1
			local.gun setAimTarget NULL
			
			local.newSpeed = 1
			//self.tankSpeed*0.5
			if (self.driving == 1) {
				self modifydrive local.newSpeed self.tankAccel 256
			}

			if (self.driving_reverse == 1) {
				self modifydrive local.newSpeed self.tankAccel 256
				self modifydrive -5 100 256
			}

			if ( self.smoke==NIL ) {
				self.smoke = spawn "models/emitters/linger_smoke.tik"
				self.smoke notsolid
				self.smoke anim start
				self.smoke.origin = (self.origin + (0 0 80))
				self.smoke glue self
			}
			local.previoushealth = self.health
			local.timeToUnStun = level.time + self.tankStunTime
		}
		
		if (local.timeToUnStun == 0) {
			// if we're not stunned, make sure we re-lock target, 
			//    in case it has changed
			self waitthread resetAimTarget
		}
		else if (local.timeToUnStun != 0 && level.time > local.timeToUnStun) {
			local.timeToUnStun = 0

			if ( local.relock_on_player==1 && self.tankTarget != NULL) {
				self waitthread resetAimTarget
			}

			if (self.driving == 1) {
				self modifydrive local.newSpeed self.tankAccel 256
			}

			if (self.driving_reverse == 1) {
				self modifydrive -local.newSpeed self.tankAccel 256
			}

			self.stunned = 0
			if (level.tankDebugPrint) { 
				dprintln "z:         " self " is no longer stunned"
			}
		}
	}
}end

//============================================================================
enemy_tank_attack_loop local.gun:  {
//============================================================================
	local.nextFireTime = 0
	while (self != NULL && self != NIL && (IsAlive self)) {
		if (self.stunned) {
			wait 0.5
		}
		else if (self.target != NULL && self.target != NIL) {
			local.gun waittill ontarget

			if ((local.nextFireTime <= level.time) &&
					((self.tankShotLimit == -1) || (self.tankShotLimit > 0))) {
				local.gun anim fire

				if (self.tankFireTrigger != NULL && self.tankFireTrigger != NIL) {
					trigger self.tankFireTrigger
				}

				local.nextFireTime = level.time + self.tankReloadTime

				if (self.tankShotLimit > 0) {
					self.tankShotLimit--
				}
			}
		}
	}
}end

//	if ( self.paused==NIL )
//		self.paused = 0
//
//	local.waited = 0
//	while (IsAlive self)
//	{
//		// get our gun on target 1st...
//		local.gun waittill ontarget
//		if ( self.paused==0 )
//		{
//			local.gun waittill ontarget
//			wait 0.25	// center on target for just a little..
//
//			local.trace_offset = local.gun.origin + (0 0 48)
//			if (self.stunned == 1)
//			{
//				//dprintln "z:             " self " is not firing because stunned"
//				wait 1
//				local.waited = 1
//			}
//			else if !(vector_within self.tankTarget.centroid local.trace_offset level.sightdistance)
//			{		
//				//dprintln "z:             " self " is not firing because range"
//				wait 1
//				local.waited = 1
//			}
//
//			// for some reason these ray casts aren't getting the player tank, even
//			//    though the player tank isn't obsured!  (DHS 4/20)
//			//else if (!(sighttrace level.playertanktarget.centroid local.trace_offset 1))
//			//{
//			//	//dprintln "z:             " self " is not firing because sight trace"
//			//	wait 1
//			//	local.waited = 1
//			//}
//			else
//			{
//				//dprintln "z:         " self " on target, firing!"
//				// wait 3
//
//				if ( local.waited==0 )
//				{
//					wait 2
//					local.waited = 1
//				}
//
//				if (self.stunned != 1) {
//					local.gun anim fire
//				}
//				wait 5
//			}
//		}
//		else
//		{
//			wait 1
//		}
//	}
//end

//============================================================================
resetAimTarget: {
//============================================================================
	if (self.tankTarget != NULL && self.tankTarget != NIL) {
		if ($(self.tankTarget).size > 1) {
			local.tankTarget = $(self.tankTarget)[1 + (randomint $(self.tankTarget).size)]
		}
		else {
			local.tankTarget = $(self.tankTarget)
		}

		if (local.tankTarget != NULL && local.tankTarget != NIL) {
			self.gun setAimTarget local.tankTarget
		}
	}
}end

//============================================================================
spawn_fx local.fx: {
//============================================================================
	local.temp = spawn script_model model local.fx
	local.temp notsolid
	local.temp.origin = self.origin
	local.temp anim start
	local.temp notsolid
	wait 5

	local.temp remove
}end

//============================================================================
spawn_damaged local.model: {
//============================================================================
	local.damaged = spawn script_model model local.model
	local.damaged.origin = self.origin
	local.damaged.angles = self.angles
	local.damaged notsolid
}end local.damaged

//============================================================================
spawn_damaged_new local.model local.angles local.origin: {
//============================================================================
	local.damaged = spawn script_model model local.model
	local.damaged.origin = local.origin
	local.damaged.angles = local.angles
}end local.damaged

//============================================================================
damaged_collision local.angles local.origin local.brushmodel: {
//============================================================================
	local.collision = spawn script_object model local.brushmodel
	local.collision.origin = local.origin
	local.collision.angles = local.angles
	local.collision safesolid
}end local.collision
