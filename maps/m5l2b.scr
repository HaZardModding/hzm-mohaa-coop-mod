// TANK DRIVE 2 
// ARCHITECTURE: STEVE/ADAM/NATE
// SCRIPTING: Z/AC
//============================================================================
main:{
//============================================================================
	level.coop_aaMap = 1					//Pasted by Criminal for coop comp. - let global scripts know this is a AA level
	level.coop_debugSpawn = 1 				//Pasted by Criminal for coop comp. - enable spawnpoint debug (make visible)
	waitthread coop_mod/main.scr::main     //Pasted by Criminal for coop comp. - start coop mod extensions
	
	//chrissstrahl - prevent ai from crouching and proneing
	level.aipronechance = 0
	level.aicrouchchance = 0

	level.script = "maps/m5l2b.scr"
	thread global/exploder.scr::main
	//thread global/exploder.scr::repeat

	exec global/loadout.scr maps/m5l2b.scr
	exec global/ai.scr
	level.dontdropweapons = 1
	
	//////////////////////////////////
	level waittill prespawn
	println "z:        prespawn marker"
	//////////////////////////////////

	level.cantgothatway = 0
	level.bombnum = 0
	level.tempnum = 0
	level.sightdistance = 3314

	level.enemytankspeed = 200
	level.enemytankspeed_reverse = -175
	level.halftrakspeed = 300
	level.lookahead = 256
	level.playertanktarget = 0

	level.fastspeed = 300
	level.slowspeed = 160

	$playertank waitthread global/vehicles_thinkers.scr::players_tank $playertank_trigger
	$playertank vehiclespeed level.fastspeed
	level.currently_fast = 1

	local.flaks[0] = $flak88_1
	local.flaks[1] = $flak88_2
	local.flaks[2] = $flak88_3
	local.flaks[3] = $flak88_4
	local.flaks[4] = $flak88_5

	exec global/randomorder.scr local.flaks

	local.flaks[0] thread global/vehicles_thinkers.scr::enemy_flak_think
	local.flaks[1] thread global/vehicles_thinkers.scr::enemy_flak_think
	local.flaks[2] thread global/vehicles_thinkers.scr::enemy_flak_think
	local.flaks[3] thread remove_flak
	local.flaks[4] thread remove_flak

	$s2_tank1	thread global/vehicles_thinkers.scr::enemy_tank_think 0 panzer
	$s10_tank1	thread global/vehicles_thinkers.scr::enemy_tank_think 0
	$s10_tank2	thread global/vehicles_thinkers.scr::enemy_tank_think 0
	$s15_tank1	thread global/vehicles_thinkers.scr::enemy_tank_think 0 panzer
	$s15_tank2	thread global/vehicles_thinkers.scr::enemy_tank_think 0 panzer
	$s20_tank1	thread global/vehicles_thinkers.scr::enemy_tank_think 0

	$mg42_1 thread global/mg42.scr::JEEP_SETUP $mg42_1_trigger1 0 $mg42_1_gunner level.playertanktarget
	level.mg42_1 = parm.previousthread

	$mg42_2 thread global/mg42.scr::JEEP_SETUP $mg42_2_trigger1 0 $mg42_2_gunner level.playertanktarget
	level.mg42_2 = parm.previousthread

	thread global/barrel.scr::explosive_barrel
	exec global/ambient.scr m5l2b

	//[200] Smithy - stop small arms destroying buildings
	//that use exploder triggers as the entry point
	//and tanks 
	//----------------------------------------------//
	//rocket is what the tank uses as MOD. 
	//this means zookas can destroy them.
	//maybe that isn't a bad thing.
	//if it is a bad thing more can be done in future
	$explodertrigger thread coop_immuniseFromBullets
	//do tanks/88s too
	$s2_tank1 		thread coop_immuniseFromBullets
	$s10_tank1 		thread coop_immuniseFromBullets
	$s10_tank2 		thread coop_immuniseFromBullets
	$s15_tank1 		thread coop_immuniseFromBullets
	$s15_tank2 		thread coop_immuniseFromBullets
	$s20_tank1 		thread coop_immuniseFromBullets
	local.flaks[0] 	thread coop_immuniseFromBullets
	local.flaks[1] 	thread coop_immuniseFromBullets
	local.flaks[2] 	thread coop_immuniseFromBullets
	//----------------------------------------------//

	//////////////////////////////////
	level waitTill spawn
	println "z:        level waittill spawn marker"
	//////////////////////////////////

	//*** telling the AI to not drop health
	level.nodrophealth = 1

	//$player stufftext "tmstartloop sound/music/mus_TigerTank_01a.mp3"			// Criminal.
	exec coop_mod/replace.scr::tmstartloop "sound/music/mus_TigerTank_01a.mp3" 	// Criminal - coop compabitility.

	//[200] Smithy - ffs, it's a trigger_once but because we have checks for pedestrians we need to make it a multiple
	//swap them out and point it to the same thread name
	if (level.gametype != 0){ 
		local.oldTrig = $level_end
		local.newTrig = spawn trigger_multiple origin (local.oldTrig.origin + (0 0 -128)) targetname "level_end"
		local.newTrig setsize ( -512 -512 0) ( 512 512 512 )
		local.newTrig setthread "level_end"
		local.oldTrig delete
	}

	//	waitthread global/objectives.scr::reset_objectives
	//	waitthread global/objectives.scr::blank_objectives
	waitthread global/objectives.scr::add_objectives 1 2 "Escape with the King Tiger tank." $level_end[1].origin //[205] chrissstrahl - make sure we only grab one not the array at all times
	waitthread global/objectives.scr::current_objectives 1
	
	//chrissstrahl - coop mod manager making sure players don't get stuck or divate to far from the mission
	thread coop_teleportMaster
}end

//============================================================================
remove_flak:{
//============================================================================
	println "z: self.basename: " self.basename

	if ($(self.basename) != NULL){
		self.basename remove
	}
	self remove
}end

//============================================================================
speedup:{
//============================================================================
	//[201] Smithy - validate it's the tank
	//[201] Smithy - now use replace method
	waitthread coop_mod/replace.scr::validateTriggerActivator "speedup" level.coop_playerTank "trigger_multiple" "coop_tankTrigger"

	if (level.currently_fast == 1)	{end}
	println  "z:        speeding up"

	for (local.speed = level.slowspeed; local.speed <= level.fastspeed; local.speed += 1){
		$playertank vehiclespeed local.speed
		waitframe
	}

	println  "z:        done speeding up"
	level.currently_fast = 1
}end

//============================================================================
slowdown:{
//============================================================================
	//[201] Smithy - validate it's the tank
	//[201] Smithy - now use replace method
	waitthread coop_mod/replace.scr::validateTriggerActivator "slowdown" level.coop_playerTank "trigger_multiple" "coop_tankTrigger"

	if (level.currently_fast != 1)	{end}
	println  "z:        slowing down"

	for (local.speed = level.fastspeed; local.speed >= level.slowspeed; local.speed -= 1){
		$playertank vehiclespeed local.speed
		waitframe
	}

	println  "z:        done slowing down"
	level.currently_fast = 0
}end

//============================================================================
ambush1:{
//============================================================================
	local.ambush_spawns = 4

	for (local.i = 1; local.i <= local.ambush_spawns; local.i++){
		if ( (randomint 100) < 60){
			thread global/ai.scr::spawn local.i
			println "z:     ambush 1 spawn: " local.i
			wait .1
		}
	}
}end

//============================================================================
s2:{
//============================================================================
//called by the BSP
//enemy tank drives back and forth across the stream

	//[201] Smithy - validate it's the tank
	//[201] Smithy - now use replace method
	waitthread coop_mod/replace.scr::validateTriggerActivator "s2" level.coop_playerTank "trigger_multiple" "coop_tankTrigger"	
	
	while (isAlive $s2_tank1){
		$s2_tank1 waitthread drive_path $s2_tank1_forward level.enemytankspeed
		if !(isAlive $s2_tank1)	{end}
		$s2_tank1 waitthread drive_path_reverse $s2_tank1_reverse level.enemytankspeed_reverse
	}
}end

//============================================================================
s9:{
//============================================================================
//called by the BSP
//enemy tank by fork in the road

	//[201] Smithy - validate it's the tank
	//[201] Smithy - now use replace method
	waitthread coop_mod/replace.scr::validateTriggerActivator "s9" level.coop_playerTank "trigger_multiple" "coop_tankTrigger"	

	if (isAlive $s10_tank2){
		$s10_tank2 thread drive_path $s10_tank2_path level.enemytankspeed_reverse
	}
}end


//============================================================================
s10:{
//============================================================================
//called by the BSP
//enemy tank by guard tower

	//[201] Smithy - validate it's the tank
	//[201] Smithy - now use replace method
	waitthread coop_mod/replace.scr::validateTriggerActivator "s10" level.coop_playerTank "trigger_multiple" "coop_tankTrigger"
	
	if (isAlive $s10_tank1){
		$s10_tank1 thread drive_path $s10_tank1_path level.enemytankspeed
	}
}end

//============================================================================
s15:{
//============================================================================
//called by the BSP
//two enemy tanks drive from the town

	//[201] Smithy - validate it's the tank
	//[201] Smithy - now use replace method
	waitthread coop_mod/replace.scr::validateTriggerActivator "s15" level.coop_playerTank "trigger_multiple" "coop_tankTrigger"

	if (isAlive $s15_tank1){
		$s15_tank1 thread drive_path $s15_tank1_path level.enemytankspeed
	}
	if (isAlive $s15_tank2){
		$s15_tank2 thread drive_path $s15_tank2_path level.enemytankspeed
	}
}end

//============================================================================
s20:{
//============================================================================
//called by the BSP
//one on the road in town

	//[201] Smithy - validate it's the tank
	//[201] Smithy - now use replace method
	waitthread coop_mod/replace.scr::validateTriggerActivator "s20" level.coop_playerTank "trigger_multiple" "coop_tankTrigger"
	
	wait 3
	if (isAlive $s20_tank1){
		$s20_tank1 thread drive_path $s20_tank1_path level.enemytankspeed
	}
}end

//============================================================================
remove_mg1:{
//============================================================================
	thread kill_mg_1
}end

//============================================================================
remove_mg2:{
//============================================================================
	//chrissstrahl - coop mod - allow players now to pass first soon to be gravestones area
	if(level.gametype != 0){ level.coop_entredTownRightWay = 1 }

	if ($mg42_2){
		thread kill_mg $mg42_2_gunner $mg42_2 $mg42_2_bipod
	}
	if (level.mg42_2){
		level.mg42_2 delete
	}
}end

//============================================================================
kill_mg_1:{
//============================================================================
//called by BSP
	if ($mg42_1){
		thread kill_mg $mg42_1_gunner $mg42_1 $mg42_1_bipod
	}
	if (level.mg42_1){
		level.mg42_1 delete
	}
}end

//============================================================================
kill_mg_2:{
//============================================================================
//called by barrel script
	wait .1
	exec global/exploder.scr::explode 11

	if ($mg42_2){
		thread kill_mg $mg42_2_gunner $mg42_2 $mg42_2_bipod
	}
	
	if (level.mg42_2){
		level.mg42_2 delete
	}

	if ($tower == NULL)	{end}

	$tower notsolid
	$tower time 1
	$tower rotatex -100
	$tower waitmove
	$tower remove
}end

//============================================================================
kill_mg local.gunner local.gun local.bipod:{
//============================================================================
	local.gun remove
	local.bipod remove

	//[203] chrissstrahl - sometimes a gunner would stay alive and be molestering players
	//gunner is invisible and sometimes has a weapon and sometimes not
	//sometimes attacking players and sometimes not
	//new debugging tools just implemented helped to reval that issue
	//tunrning the hidden ai visible as it touched the developer
	//[205] chrissstrahl - fix - cannot give health to dead entities
	if(local.gunner){
		if(isalive local.gunner){
			local.gunner takedamage
			local.gunner health 0.01
			local.gunner exec global/bullethit.scr (0 0 250) 500 0 1
		}
		local.gunner hide
	}
}end

//============================================================================
level_end:{
//============================================================================
//called by the BSP
	if (level.missionfailed == 1){end}

	//[200] Smithy - only tanks allowed to end level
	if (level.gametype != 0){ //duh, check gametype...
		local.player = parm.other
		if (!level.coop_playerTank || local.player != level.coop_playerTank){ //thanks chris for this level var ! :D
			if (!local.player.flags["coop_tankEndHint"]){  //damn... i was tired yesterday... it goes here not in an else...
				local.player.flags["coop_tankEndHint"] = game.true
				local.player iprint "The tank needs to reach here to win!" 1
			}
			end
		}
	}

	waitthread global/objectives.scr::add_objectives 1 3
	exec global/missioncomplete.scr m5l3
}end

//============================================================================
drive_path local.path local.speed local.remove:{
//============================================================================
// drive Vector position, speed, acceleration, reach_distance, look_ahead
//println "z:              " local.path
//println "z:              " local.speed
	self.driving = 1
	self drive local.path local.speed 30 200 level.lookahead
	//self modifydrive level.fastspeed 50 level.lookahead

	self waitTill drive
	if (self){
		self stop
		self.driving = 0
		if (local.remove == "remove"){
			self remove
		}
	}
}end

//============================================================================	
drive_path_reverse local.path local.speed:{
//============================================================================
// drive Vector position, speed, acceleration, reach_distance, look_ahead
//println "z:              " local.path
//println "z:              " local.speed
	self.driving_reverse = 1
	self drive local.path local.speed 30 200 level.lookahead
	//self modifydrive level.fastspeed 50 level.lookahead

	self waitTill drive
	self stop
	self.driving_reverse = 0
}end

//============================================================================
cant_go_that_way:{
//============================================================================
//called by the BSP
	//chrissstrahl - coop disqualifier, only do this is tank is heading this way
	local.player = parm.other
	if(level.gametype == 0 || local.player == level.coop_playerTank ){
		print("")
	}else{
		end
	}

	if( level.drive == NIL || level.drive.size == 0){
		level.drive[0] =  dfr_drive_01h_2
		level.drive[1] =  dfr_drive_02h_1
		level.drive[2] =  dfr_drive_04h_1
		level.drive[3] =  dfr_drive_05h_1
		level.drive[4] =  dfr_drive_07h_2
		level.drive[5] =  dfr_drive_08h_1
		level.drive[6] =  dfr_drive_09h_1
		level.drive[7] =  dfr_drive_10h_2
	
		if (level.prev_drive != NIL){
			for (local.i = 0; local.i < level.drive.size; local.i++){
				if (level.drive[local.i] == level.prev_drive){
				//println "before1: " level.drive.size
				level.drive[local.i] = NIL
				//println "after1: " level.drive.size
				break
				}
			}
		}
	}

	level.prev_drive = thread get_random_entry level.drive
	//level.playertank playsound level.prev_drive
	//$player playsound level.prev_drive					//Criminal.
	//exec coop_mod/replace.scr::playsound level.prev_drive	//Criminal - Coop compabitility. //chrissstrahl
	local.player playsound level.prev_drive //chrissstrahl - coop
}end

//============================================================================
get_random_entry local.entries:{
//============================================================================
	local.n = randomint (local.entries.size)
	//println "XXX:" local.n
	local.entry = 0
	get_random_entry_loop:

	local.result = local.entries[local.entry]
	if (local.result == NIL){
		local.entry++
		goto get_random_entry_loop
	}
	if (local.n){
		local.n--
		local.entry++
		goto get_random_entry_loop
	}
	
	//println "result: " local.result
	local.entries[local.entry] = NIL
}end local.result


//chrissstrahl - executes the actual teleportation of the brothers in arms
//============================================================================
coop_teleportBrother local.origin local.angle local.message:{
//============================================================================
	if(local.origin != NIL){
		local.origin[2] += 300 //from thy havens me angels shall fall
		self origin local.origin //brother there lies your desteny
		self droptofloor //like a fallen angel
		if( local.angle != NIL ){
			self face ( local.angle )//face your path chosen by your maker
		}
		//[200] chrissstrahl - make sure players do not get stuck
		thread coop_mod/main.scr::playerMakeSolidAsap self //[201] Smithy - added func location
	}
	if(local.message != NIL){	self iprint local.message 1	}
}end //AMEN Brother!

//chrissstrahl - this makes sure players stay on track, in the map, don't fall off or get stuck
//============================================================================
coop_teleportMaster:{
//============================================================================
	if(level.gametype == 0){ end }
	
	local.message = "Return to Battle, your Brethren need you!"
	//end
	while(1){
		for (local.i = 1;local.i <= $player.size;local.i++){
			local.messagePrintout = NIL
			local.player =  $player[local.i]
			if( local.player != NULL ){
				if(  local.player != level.coop_playerTank && local.player.health > 0 && local.player.dmteam != "spectator" && local.player.flags["coop_isActive"] == 1 ){
					local.plOrigin = local.player.origin
					//if(vector_length(local.plOrigin - ()) < 2000){
					
					//left side at start bridge
					if(local.plOrigin[0] > 700 && local.plOrigin[0] < 3324 && local.plOrigin[1] > -8000 && local.plOrigin[1] < -6400 ){
						local.player thread coop_teleportBrother ( 2711 -5864 -71 ) (0 85 0) local.message
					}
					//first soon to be gravestones
					if(level.coop_entredTownRightWay == NIL && local.plOrigin[0] < 56 && local.plOrigin[0] > -272 && local.plOrigin[1] > -584 && local.plOrigin[1] < 256 ){
						local.player thread coop_teleportBrother ( 386 -91 -129 ) (0 30 0) local.message
					}
					//first soon to be gravestones from the other side
					if(local.plOrigin[0] < -150 && local.plOrigin[0] > -370 && local.plOrigin[1] < 350 && local.plOrigin[1] > 515 ){
						local.player thread coop_teleportBrother ( -777 -167 -53 ) (0 -178 0) local.message
					}
					//second soon to be gravestones with the flak and the tower with explosives barrels on the intersection
					if(local.plOrigin[0] < 1488 && local.plOrigin[0] > 703 && local.plOrigin[1] > 6200 && local.plOrigin[1] < 6600 ){
						local.player thread coop_teleportBrother ( 627 6066 -132 ) (0 -173 0) local.message
					}
					//leftside town entrance buildings
					if(local.plOrigin[0] > -3377 && local.plOrigin[0] < -3148 && local.plOrigin[1] < 1311 && local.plOrigin[1] > 975 ){
						local.player thread coop_teleportBrother ( -3619 1052 35 ) (0 -122 0) local.message
					}
					//leftside town entrance buildings
					if(local.plOrigin[0] > -3040 && local.plOrigin[0] < -2000 && local.plOrigin[1] < 2950 && local.plOrigin[1] > 2150 ){
						local.player thread coop_teleportBrother ( -3706 2570 -50 ) (0 -122 0) local.message
					}
					//leftside town entrance building 2
					if(local.plOrigin[0] > -3408 && local.plOrigin[0] < -3149 && local.plOrigin[1] < 1645 && local.plOrigin[1] > 1357 ){
						local.player thread coop_teleportBrother ( -3539 1447 5 ) (0 -122 0) local.message
					}
					//leftside scettchy corner before town entrance
					if(local.plOrigin[0] < 1488 && local.plOrigin[0] > 703 && local.plOrigin[1] > 6200 && local.plOrigin[1] < 6600 ){
						local.player thread coop_teleportBrother ( 627 6066 -132 ) (0 -173 0) local.message
					}
					//left side away from gravestones
					//buildings group 1 next to first soon to be gravestones
					if(local.plOrigin[0] < -808 && local.plOrigin[0] > -1650 && local.plOrigin[1] < -554 && local.plOrigin[1] > -1588 ){
						local.player thread coop_teleportBrother ( -1669 -470 -5 ) (0 170 0) local.message
					}
					//buildings group 2 next to first soon to be gravestones
					if(local.plOrigin[0] < -1683 && local.plOrigin[0] > -2650 && local.plOrigin[1] < -647 && local.plOrigin[1] > -1588 ){
						local.player thread coop_teleportBrother ( -2511 -462 2 ) (0 173 0) local.message
					}
					//buildings group 3 next to first soon to be gravestones
					if(local.plOrigin[0] < -2700 && local.plOrigin[0] > -3000 && local.plOrigin[1] < -559 && local.plOrigin[1] > -1588 ){
						local.player thread coop_teleportBrother ( -2932 -450 12 ) (0 173 0) local.message
					}
					//buildings group 4 next to first soon to be gravestones
					if(local.plOrigin[0] < -3030 && local.plOrigin[0] > -3360 && local.plOrigin[1] < -591 && local.plOrigin[1] > -1588 ){
						local.player thread coop_teleportBrother ( -3350 -450 26 ) (0 173 0) local.message
					}
					//buildings group 5 next to first soon to be gravestones
					if(local.plOrigin[0] < -4510 && local.plOrigin[0] > -4830 && local.plOrigin[1] < -750 && local.plOrigin[1] > -1588 ){
						local.player thread coop_teleportBrother ( -4743 -495 44 ) (0 166 0) local.message
					}
					//right side gap between buildings next to final flak
					if( vector_length(local.plOrigin - ( -6760 318 40 )) < 100 ){
						local.player thread coop_teleportBrother ( -6873 48 42 ) (0 -111 0) local.message
					}
					//right side gap between buildings next to end flak but further back towatrds start pos
					if( vector_length(local.plOrigin - ( -6128 395 0 )) < 100 ){
						local.player thread coop_teleportBrother ( -6122 218 67 ) (0 -143 0) local.message
					}
					//buildings group right side end flak distance 1
					if(local.plOrigin[0] > -5823 && local.plOrigin[0] < -5485 && local.plOrigin[1] < 630 && local.plOrigin[1] > 400 ){
						local.player thread coop_teleportBrother ( -5672 252 49 ) (0 -145 0) local.message
					}
					//buildings group right side end flak distance 2
					if(local.plOrigin[0] > -5439 && local.plOrigin[0] < -5145 && local.plOrigin[1] < 630 && local.plOrigin[1] > 370 ){
						local.player thread coop_teleportBrother ( -5324 266 56 ) (0 -157 0) local.message
					}
					//buildings group right side end flak distance 3
					if(local.plOrigin[0] > -5100 && local.plOrigin[0] < -4805 && local.plOrigin[1] < 630 && local.plOrigin[1] > 360 ){
						local.player thread coop_teleportBrother ( -5047 271 50 ) (0 -157 0) local.message
					}
					//buildings group right side end flak distance 4
					if(local.plOrigin[0] > -3231 && local.plOrigin[0] < -2942 && local.plOrigin[1] < 630 && local.plOrigin[1] > 239 ){
						local.player thread coop_teleportBrother ( -3160 132 21 ) (0 -177 0) local.message
					}
					//buildings group right side end flak distance 5
					if(local.plOrigin[0] > -2431 && local.plOrigin[0] < -2107 && local.plOrigin[1] < 630 && local.plOrigin[1] > 270 ){
						local.player thread coop_teleportBrother ( -2384 123 10 ) (0 -176 0) local.message
					}
					//buildings group right side end flak distance 6
					if(local.plOrigin[0] > -2067 && local.plOrigin[0] < -1774 && local.plOrigin[1] < 630 && local.plOrigin[1] > 200 ){
						local.player thread coop_teleportBrother ( -2384 123 10 ) (0 -174 0) local.message
					}
					//buildings gap after group 6 right side end flak distance 6
					if(vector_length(local.plOrigin - ( -1348 374 -90 )) < 100 ){
						local.player thread coop_teleportBrother ( -1388 234 -1 ) (0 -170 0) local.message
					}
					//I lost overview
					if(vector_length(local.plOrigin - ( -7384 55 0 )) < 100 ){
						local.player thread coop_teleportBrother ( -7347 -121 44 ) (0 -84 0) local.message
					}
					//print message if there is one set
					if(local.messagePrintout != NIL){
						local.player iprint local.messagePrintout 1
					}
				}
			}
		}
		waitframe
	}
}end

//[200] Smithy - stop this from being killed by small arms
//============================================================================
coop_immuniseFromBullets:{
//============================================================================
	self immune bullet
	self immune shotgun
	self immune bash
	self immune grenade
	self immune fast_bullet
}end

