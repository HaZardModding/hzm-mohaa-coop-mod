//============================================================
InitIntro: {
//============================================================
	$spy exec global/disable_ai.scr
	$spy german
	$spy gun "none"
	$spy rendereffects "-shadow"
	//$spy anim jeep_idle_drive

	// Crimewavez = Only in SP.
	// Disguise managed in e1l4.scr, line 7.
	//[200] chrissstrahl - we are using the replacment in any case, so that if we get it working in the future all scripts are prepared
	exec coop_mod/replace.scr::has_disguise 1 //@smithy - this still valid ?

	waitthread AIdisable
	level.RideOver = 0
}end

//============================================================
AIdisable: {
//============================================================
	$clerk ai_off
	$clerkbuddy ai_off
	$boatguard  ai_off
	
	for(local.j = 1; local.j < $deleteguy2.size; local.j++) {
		$deleteguy2[local.j] ai_off
	}

	for(local.i = 1; local.i < $ai_alarm.size; local.i++) {
		$ai_alarm[local.i] ai_off
	}
}End

//============================================================
DoIntro: {
//============================================================
	if (level.skipintro == 1) {
		println "DEBUG:  skipping intro"
		thread giveDisguise
		$player.origin = "-4360 4696 96"
		waitthread AIenable
		thread DeleteGuy1
		end
	}

	drawhud 0
	thread handleDriver
	thread handlePlayer
	thread startTruckLightsCheck

	$starttruck thread startTruckOptions
	$starttruck rendereffects "+growradius"
	$starttruck drive $starttruckpath 300 200 200 128		//256

	//call to briefing dialogue
	thread doBriefing
	thread global/MaintainCamera.scr::StartMaintainCamera $starttruck
	thread handleTruckGag
		
	//wait until the briefing is done before we continue
	waitframe
	$starttruck waittill drive

	waitthread GateGuardDialogue
	
	//[200] chrissstrahl - we can't trust a player to be there, so open differently
	//$gate1 open $player
	$gate1 open $starttruck

	wait 2.0
	$starttruck drive $gatetruckpath 300 200 200 128		//256

	wait 5
	$gate1 close

	//waitframe
	$starttruck waittill drive
	waitthread doBriefing2
	level.Introdrivedone = 1	

	thread DontBeCaught
	
	// Hand the player the gun
	//[200] chrissstrahl - force weapon in coop
	//@Smithy - can u do me a list for stuff like give take weapons and stelth and papers then i can do them in the future
	if(level.gametype == 0){
		$player item weapons/silencedpistol.tik  
		$player ammo pistol 16	
	}else{
		//level.coop_forceSecondary = "weapons/silencedpistol.tik" 							//[202] Smithy - not needed
		//[200] chrissstrahl - give item/ammo and make player use it right away
		//thread coop_mod/replace.scr::item "weapons/silencedpistol.tik" 1 					//[202] Smithy - not needed		
		thread coop_mod/replace.scr::givePlayerWeapon "models/weapons/silencedpistol.tik" 	//[202] Smithy - give current and future players the weapon
		thread coop_mod/replace.scr::ammo pistol 32
	}
	
	$starttruck setcollisionentity $truckcollide
	thread maps/e1l4/PreShip.scr::DontMoveUnlessIAmNear
}end

//============================================================
doBriefing: {
//============================================================
	$spy say A_14_ClausTalk01   //we are going to the...
	$spy waittill saydone
	$spy say A_14_ClausTalk02  //we must have those plans...
	$spy waittill saydone
	$spy say A_14_ClausTalk03  //sink da boat...
	$spy waittill saydone
}end

//============================================================
doBriefing2:{
//============================================================
	$spy say A_14_ClausTalk04  //these papers will...
	$spy waittill saydone
	$spy say A_14_Klaus_1A  // after you sabotage...
	$spy waittill saydone
}end
	
//============================================================
DontBeCaught: {
//============================================================
	for(local.i = 1; local.i <= $deleteguy2.size; local.i++) {
		$deleteguy2[local.i] dontdropweapons
	}
}End

//============================================================
IfPullGunFail: {
//============================================================
	self dontdropweapons
	//@smithy - not sure if that will work right, we will have to keep an eye on it
	while(self && (self.thinkstate != "attack" ||  self.thinkstate != attack)) {
		wait 1.0

		if(level.failongun == 0) { End }
	}

	if(self != NIL && self != NULL) {
		thread FAILEDMISSION
	}
}End

//============================================================
FAILEDMISSION: {
//============================================================
	if(level.FAILEDMISSION == 1) {
		End
	}

	level.FAILEDMISSION = 1
	level.sirenson = 1
	wait 2
	
	iprintln "You've blown your cover without getting on the ship!"
	wait 3
	
	iprintln "MISSION FAIL"
	missionfailed
	
	//@chrissstrahl - make sure it loads the same map again
}End

//============================================================
DeleteGuy1: {
//============================================================
	thread global/EntityCleanup.scr::EntityCleanup "pathintalkers0" 1
	thread global/EntityCleanup.scr::EntityCleanup "t375" 1
	thread global/EntityCleanup.scr::EntityCleanup "t375_passenger" 1
	thread global/EntityCleanup.scr::EntityCleanup "deleteguy1" 1
	thread global/EntityCleanup.scr::EntityCleanup "1stgateguard" 1
}End

//============================================================
GateGuardDialogue: {
//============================================================
	// Crimewavez - only in SP, again.
	if(level.gametype == 0) {
		// "press 7 to show your papers" throbbing text...	
		local.key = getboundkey1 "toggleitem"
		local.string = (loc_convert_string ("Press " + local.key + " to show your papers."))
		thread global/throbtext.scr::throbtext local.string 9999
	}
	//[200] chrissstrahl - show this also in coop
	else{
		local.string = (loc_convert_string ("Show your papers.")) //bitch! :D
		thread global/throbtext.scr::throbtext local.string 9999
	}
	//[200] chrissstrahl - allow player to show papers
	exec coop_mod/replace.scr::physics_on

	$starttruck setcollisionentity $truckcollide2


	//@chrissstrahl - make compatrible
	local.placetospawn = $player.origin + (0 -1000 0)
	local.person = spawn models/human/german_wehrmact_soldier.tik origin local.placetospawn "targetname" "mrinvisible2"
	local.person hide
	
	//@chrissstrahl //@smithy - this needs to be removed this is just here to speed up testing
	setcvar "timescale" 1 //[202] chrissstrahl - slow down for player to show papers

	$mrinvisible2.shuddup = 1
	$mrinvisible2 exec global/enable_ai.scr
	$mrinvisible2 notsolid
	$mrinvisible2 exec coop_mod/replace.scr::turnto //[202] chrissstrahl
	$mrinvisible2.origin = $spy.origin + (0 0 -50)
	$mrinvisible2 waitthread askForPapers

	thread KillPlayerIfFailPapers

	//$1stgateguard playsound DFRGM_E1L4_CK3408 //use your uniform...
	$1stgateguard turnto $spy
    //$1stgateguard waittill turntodone
	$1stgateguard say A_14_GermATalk1    //I need to see papers for both of you
	$1stgateguard waittill saydone

	$spy anim A_14_Claus_show_papers
	$spy waittill animdone
	$spy anim A_14_Claus_Opel_idle

	if(level.GatePapersAccepted != 1) {
		$1stgateguard say A_14_GermATalk2    //and you sir?
		$1stgateguard waittill say
	}

	//$1stgateguard waitthread askForPapers
	//thread keepAskingtillSeePapers
	while(level.GatePapersAccepted != 1) { wait 0.3 }
	
	//@chrissstrahl //@smithy - this needs to be removed this is just here to speed up testing
	setcvar "timescale" 10 //[202] chrissstrahl - speedup testing
	
	//@smithy - I think this paper stuff needs checking
	//[200] chrissstrahl - make sure players work fine with being glued again
	exec coop_mod/replace.scr::physics_off 

	$1stgateguard anim A_14_GermATalk4    //if you want to board the boat you will have to see the HM
	$1stgateguard waittill animdone
	$spy lookat NULL
	//$spy waitthread global/LoopAnim.scr::LoopAnimStop
    //$spy anim jeep_idle_drive //00A004_driversitstill
	$mrinvisible2 remove
}End

//============================================================
KillPlayerIfFailPapers: {
//============================================================
	//while ($1stgateguard && (isalive $1stgateguard) &&
	//($1stgateguard.thinkstate != "attack")) { wait 1 }
	//[200] chrissstrahl - make this less confusing
	while ( (isalive $1stgateguard) && ($1stgateguard.thinkstate != "attack")) {
		wait 1
	}
	
	if (!$1stgateguard || !(isalive $1stgateguard)) { end }
	wait 1

	//@smithy
	if (level.GatePapersAccepted != 1){
		waitthread global/throbtext.scr::throbtext_off
		waitframe
		
		//[202] chrissstrahl - coop compatibility
		exec coop_mod/replace.scr::killplayer
		
		//[202] chrissstrahl - fail mission in coop
		if(level.gametype != 0){
			exec coop_mod/replace.scr::missionfailed
		}
	}
}end

//============================================================
keepAskingtillSeePapers: {
//============================================================
	//	thread HACK
	while(level.GatePapersAccepted != 1) {
		$1stgateguard anim A_14_GermATalk3
		$1stgateguard waittill animdone
		wait 10
	}
}End

//============================================================
HACK: {
//============================================================
	wait 6
	if(level.GatePapersAccepted != 1) {
		level.GatePapersAccepted = 1
	}
}End

//@smithy
//============================================================
askForPapers: {
//============================================================
	self disguise_level 1
	self disguise_period 60
	self disguise_range 3000
	self type_attack alarm
	self type_disguise sentry
	self disguise_accept_thread papersAccepted
	
	//wait 2
	self exec global/enable_ai.scr
}end

//============================================================
papersAccepted: {
//============================================================
	thread global/throbtext.scr::throbtext_off
	level.GatePapersAccepted = 1
	waitthread AIenable
}end

//============================================================
AIenable: {
//============================================================
	$clerk ai_on
	$clerkbuddy ai_on
	$boatguard  ai_on
	
	for(local.j = 1; local.j < $deleteguy2.size; local.j++) {
		$deleteguy2[local.j] ai_on
	}

	for(local.i = 1; local.i < $ai_alarm.size; local.i++) {
		$ai_alarm[local.i] ai_on
	}
}End

//============================================================
giveDisguise: {
//============================================================
	// Crimewavez - coop comp.
	// Disguise managed in e1l4.scr, line 7.
	if(level.gametype == 0) {
		$player playsound pickup_papers
		$player playsound pickup_uniform
		waitthread global/items.scr::add_item "papers_level1" noMessage
		$player.has_disguise = 1
		setcvar g_playermodel "Sc_AX_Ital_Inf" 
	}
	else {
	//@smithy
		exec coop_mod/replace.scr::has_disguise  1 //[200] chrissstrahl - give disguise in coop, we might have special features in the future
		exec coop_mod/replace.scr::playsound "pickup_papers"
		exec coop_mod/replace.scr::playsound "pickup_uniform"
		waitthread global/items.scr::add_item "papers_level1" noMessage
	}
}end

//============================================================
handleDriver: {
//============================================================
	$spy physics_off
	$spy notsolid
	$spy nodamage
	$spy angles (0 0 0)
	$spy attach $starttruck driver 1 
	//	$spy anim jeep_idle_drive //00A004_driversitstill
	$spy gun "none"	
	$spy thread driverDriveStraight
}end

//animations for Claus as he drives ye olde truck 
//============================================================
driverTurnLeft local.degrees: {
//============================================================
	if (level.inLeftTrun == 1) { end }
	level.inLeftTurn = 1
	$spy anim A_13_driver_turnleft
	$spy waittill animdone
	level.inLeftTurn = 0
}end

//============================================================
driverTurnRight local.degrees: {
//============================================================
	if (level.inRightTurn == 1) { end }
	level.inRightTurn = 1
	$spy anim A_13_driver_turnright
	$spy waittill animdone
	level.inRightTurn = 0 
}end

//============================================================
driverDriveStraight: {
//============================================================
	if (level.driverDrivingStraight == 1) { end }
	level.driverDrivingStraight = 1
	$spy thread global/loopanim.scr::LoopAnim A_13_driver_drive
}end

//============================================================
driverStopDriving: {
//============================================================
	$spy thread global.loopanim.scr::LoopAnimStop
	level.driverDrivingStraight = 0
}end

//@smithy
//============================================================
handlePlayer: {
//============================================================
	thread giveDisguise
	level.opelpassengerseat = spawn script_origin
	local.seatpos = $starttruck gettagposition "passenger"
	local.seatpos[2] -= 45
	level.opelpassengerseat.origin = local.seatpos
	level.opelpassengerseat bind $starttruck

	//Crimewavez - coop comp, see replace.scr and SDK in case of questions.
	if(level.gametype == 0) {
		$player nodamage
		$player notsolid
		//	$player physics_off
		$player glue level.opelpassengerseat
		$player.viewangles = (0 180 0)
	}
	else {
		for(local.i = 1; local.i <= $player.size; local.i++){
			local.player = $player[local.i]
			local.player nodamage
			local.player notsolid
		}
		exec coop_mod/replace.scr::glue level.opelpassengerseat
		exec coop_mod/replace.scr::viewangles (0 180 0)
	}
}end

//============================================================
startTruckLightsCheck:	{
//============================================================
	if (level.startTruckLights == 1 && level.RideOver == 0) {
		$starttruck vehicleanim idlelights
	}
	else {
		$starttruck vehicleanim idlenolights 
	}
}end

//============================================================
handleTruckGag:	 {
//============================================================
    thread intro_truck_jitter

	while(level.Introdrivedone != 1) { wait 0.2 }
	
	$starttruck.corona = 0  //turn off coronas
	waitframe

	$starttruck moveanim passengerdooropen
	//[202] chrissstrahl - coop compatibility
	//exec coop_mod/replace.scr::playsound  opeltruck_snd_dooropen //sound when player opens door
	//$player playsound opeltruck_snd_dooropen //sound when player opens door
	$starttruck playsound opeltruck_snd_dooropen //sound when player opens door
	wait 0.5
	
	//[202] chrissstrahl - coop compatibility
	$starttruck playsound opeltruck_snd_doorclose  //sound when player closes door
	waitthread DeleteGuy1
	thread PlayerExitVehicle
	level.RideOver = 1

	thread startTruckLightsCheck

	// take care of Klaus driving out
	if ($starttruckleavepathA && $starttruckbackuppath && $starttruckleavepathB) {
		wait 3

		local.speed = 400
		local.reverse_speed = -0.5 * local.speed
		local.accel = 400
		local.reached_dest = 200
		local.look_ahead = 256

		$starttruck drive $starttruckbackuppath local.reverse_speed local.accel local.reached_dest local.look_ahead
		$starttruck waittill drive
		wait 0.5

		$starttruck drive $starttruckleavepathA local.speed local.accel local.reached_dest local.look_ahead
		wait 0.5

		//[202] chrissstrahl - coop compatibility
		//$gate1 open $player
		$gate1 open $world
		
		$starttruck waittill drive
		$starttruck drive $starttruckleavepathB local.speed local.accel local.reached_dest local.look_ahead
		wait 1

		$gate1 close
		//$starttruck waittill drive
	}
}end

//============================================================
startTruckOptions: {	//self refers to $starttruck
//============================================================
	$starttruck nodamage

	if (level.startTruckLights == 1) {
		exec global/spotlight.scr::corona $starttruck "light left" // Turn on headlight coronas
		exec global/spotlight.scr::corona $starttruck "light right"
	}
}end

//============================================================
PlayerExitVehicle: {
//============================================================
	//@chrissstrahl //@smithy - this needs to be removed this is just here to speed up testing
	setcvar "timescale" 1 //[202] chrissstrahl - speedup testing

	//[202] chrissstrahl - handle unglue crap
	local.player = exec coop_mod/replace.scr::player_closestTo $starttruck
	if(!local.player){
		goto PlayerExitVehicle_skipunglueing
	}
	
	local.startpos = local.player.origin
	local.exitpos = $starttruck gettagposition "passenger_enter"
	local.exitpos[2] = local.startpos[2]

	thread global/MaintainCamera.scr::StopMaintainCamera
	
	$starttruck notsolid
	local.playerspot = spawn script_origin origin local.startpos	
	
	//[202] chrissstrahl - unglue player, then glue to prevent messing up truck
	if(level.gametype==0){
		$player unglue
		$player solid
		$player physics_on
		$player takedamage
		$player.origin = local.exitpos
		$player glue local.playerspot 0
	}else{
		exec coop_mod/replace.scr::unglue local.exitpos NIL
		exec coop_mod/replace.scr::glue local.playerspot 0
	}
	
	local.disembark_time = 0.8
	local.playerspot time local.disembark_time
	local.playerspot moveto local.exitpos
	local.playerspot move
	wait local.disembark_time
	
	println("PlayerExitVehicle unglueing")
	
	//[202] chrissstrahl - unglue again
	exec coop_mod/replace.scr::unglue NIL NIL
	exec coop_mod/replace.scr::physics_on
	
	println("PlayerExitVehicle unglueing done")
	
	//[202] chrissstrahl - unglueing is skipped if no player was theer to get origins
	PlayerExitVehicle_skipunglueing:
	local.playerspot remove
	println("PlayerExitVehicle unglueing skip")
	
	drawhud 1
}end

//============================================================
OpenGate1 local.vehicle: {
//============================================================
	//iprintln "OpenGate1 Trigger lives!!!"
	//$starttruck fullstop
	end
	
	if (local.vehicle == $starttruck) {
		self remove

		$starttruck modifydrive 0 500 156
		
		//[202] chrissstrahl - coop compatibility
		//$gate1 open $player
		$gate1 open $world
		
		wait 2.5

		$starttruck modifydrive 500 200 156
	}
}end

//============================================================
intro_truck_jitter: {
//============================================================
	while (level.RideOver != 1) {
		if (level.RideOver == 0) {
			waitexec global/earthquake.scr .3 .1 0 0
    	}
	
	    if (level.RideOver == 0) {
			waitexec global/earthquake.scr 3 .05 0 0
	    }

    	if (level.RideOver == 0) {
			waitexec global/earthquake.scr .5 .15 0 0
    	}

    	if (level.RideOver == 0) {
			waitexec global/earthquake.scr 1.5 .05 0 0
	    }

    	if (level.RideOver == 0) {
			waitexec global/earthquake.scr .2 .2 0 0
  		}

    	if (level.RideOver == 0) {
			waitexec global/earthquake.scr 3 .05 0 0
    	}
	}
}end